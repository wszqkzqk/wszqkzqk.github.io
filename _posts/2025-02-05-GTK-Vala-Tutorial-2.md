---
layout:     post
title:      GTK/Valaå¼€å‘åŸºç¡€æ•™ç¨‹ 2
subtitle:   ä½¿ç”¨GTK/Valaæ„å»ºç®€å•åº”ç”¨
date:       2025-02-05
author:     wszqkzqk
header-img: img/GTK-logo.webp
catalog:    true
tags:       å¼€æºè½¯ä»¶ GTK Vala
---

æœ¬æ–‡é‡‡ç”¨[**CC-BY-SA-4.0**](https://creativecommons.org/licenses/by-sa/4.0/)åè®®å‘å¸ƒï¼Œä½†æœ¬æ–‡ä»£ç é‡‡ç”¨[**LGPL v2.1+**](https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html)åè®®å…¬å¼€

# å‰è¨€

* åœ¨ä¹‹å‰çš„[GTK/Valaå¼€å‘åŸºç¡€æ•™ç¨‹](https://wszqkzqk.github.io/2023/01/19/GTK-Vala-Tutorial/)å‘å¸ƒåğŸ•Šäº†2å¹´ç»ˆäºå†™äº†ä¸€ç¯‡åç»­ã€‚ğŸ¥²ğŸ¥²ğŸ¥²

æœ¬æ–‡å‡è®¾è¯»è€…å·²ç»é˜…è¯»äº†ç¬”è€…çš„ä¸Šä¸€ç¯‡[GTK/Valaå¼€å‘åŸºç¡€æ•™ç¨‹](https://wszqkzqk.github.io/2023/01/19/GTK-Vala-Tutorial/)ï¼Œå·²ç»å¯¹GTK/Valaçš„åŸºæœ¬æ¦‚å¿µæœ‰æ‰€äº†è§£ã€‚æœ¬æ–‡å°†é€šè¿‡ç®€å•çš„ä¾‹å­ï¼Œå±•ç¤ºå¦‚ä½•ä½¿ç”¨GTK4å’ŒValaæ„å»ºä¸€ä¸ªç®€å•çš„åº”ç”¨ç¨‹åºã€‚æœ¬æ–‡çš„ä¾‹å­å‡ä¸å¤ªé˜³â˜€ï¸â˜€ï¸â˜€ï¸æœ‰å…³ã€‚

# GTK4åº”ç”¨ï¼šç™½æ˜¼æ—¶é•¿è®¡ç®—ä¸ç»˜åˆ¶å·¥å…·

ç¬”è€…åœ¨è¿™é‡Œç”¨ä¸€ä¸ªæœ‰è¶£çš„ä¾‹å­æ¥å±•ç¤ºå¦‚ä½•æ„å»ºä¸€ä¸ªç®€å•çš„åº”ç”¨ç¨‹åºã€‚è¯¥ç¨‹åºç”¨åˆ°äº†GTK4çš„è®¸å¤šç»„ä»¶ï¼ˆæœ‰æ„è¯†åœ°åœ¨ç”¨GTK4ä¸‹æ–°å¢çš„ï¼Œæ¨èçš„ï¼Œæˆ–è€…è¡Œä¸ºå‘ç”Ÿæ”¹å˜çš„ç»„ä»¶ï¼‰ï¼Œä»¥åŠCairoçš„ç»˜å›¾åŠŸèƒ½ã€‚

è¿™ä¸ªåº”ç”¨ç¨‹åºå¯ç”¨äºè®¡ç®—åœ¨ä¸åŒçº¬åº¦ä¸‹ï¼Œå…¨å¹´çš„æ¯ä¸€å¤©çš„ç™½æ˜¼æ—¶é•¿ã€‚ä¸ºäº†æ–¹ä¾¿ï¼Œç¨‹åºåšäº†è¿™äº›ç®€åŒ–ï¼š

* ä¸è€ƒè™‘å¤§æ°”æŠ˜å°„
* ä¸è€ƒè™‘è¿›åŠ¨ï¼ˆå²å·®ï¼‰
* å‡è®¾åœ°çƒå…¬è½¬ä¸è‡ªè½¬çš„è§’é€Ÿåº¦æ’å®šä¸”å®Œå…¨ä¸æ ¼é‡Œé«˜åˆ©å†å¯¹é½
* å‡è®¾è‡ªè½¬å€¾è§’æ’å®šä¸º23.44Â°

## è¿è¡Œæ•ˆæœ

|||
|:----:|:----:|
|[![#~/img/GTK-examples/beijing-day-length.webp](/img/GTK-examples/beijing-day-length.webp)](/img/GTK-examples/beijing-day-length.webp)|[![#~/img/GTK-examples/chongqing-day-length.webp](/img/GTK-examples/chongqing-day-length.webp)](/img/GTK-examples/chongqing-day-length.webp)|
|åŒ—äº¬|é‡åº†|
|[![#~/img/GTK-examples/berlin-day-length.webp](/img/GTK-examples/berlin-day-length.webp)](/img/GTK-examples/berlin-day-length.webp)|[![#~/img/GTK-examples/sydney-day-length.webp](/img/GTK-examples/sydney-day-length.webp)](/img/GTK-examples/sydney-day-length.webp)|
|æŸæ—|æ‚‰å°¼|
|[![#~/img/GTK-examples/singapore-day-length.webp](/img/GTK-examples/singapore-day-length.webp)](/img/GTK-examples/singapore-day-length.webp)|[![#~/img/GTK-examples/murmansk-day-length.webp](/img/GTK-examples/murmansk-day-length.webp)](/img/GTK-examples/murmansk-day-length.webp)|
|æ–°åŠ å¡|æ‘©å°”æ›¼æ–¯å…‹|
|[![#~/img/GTK-examples/n-polar-day-length.webp](/img/GTK-examples/n-polar-day-length.webp)](/img/GTK-examples/n-polar-day-length.webp)|[![#~/img/GTK-examples/s-polar-day-length.webp](/img/GTK-examples/s-polar-day-length.webp)](/img/GTK-examples/s-polar-day-length.webp)|
|åŒ—æç‚¹|å—æç‚¹|

## ç¨‹åºæ¶æ„

### è¾…åŠ©å‡½æ•°
- `days_in_year (int year)`  
  åˆ¤æ–­ç»™å®šå¹´ä»½æ˜¯å¦ä¸ºé—°å¹´ï¼Œè¿”å›ä¸€å¹´ä¸­çš„å¤©æ•°ã€‚  
  åˆ¤æ–­è§„åˆ™ï¼šèƒ½è¢« 400 æ•´é™¤ï¼Œæˆ–è€…èƒ½è¢« 4 æ•´é™¤ä½†ä¸èƒ½è¢« 100 æ•´é™¤çš„ä¸ºé—°å¹´ã€‚

- `solar_declination (int n)`  
  åˆ©ç”¨è¿‘ä¼¼å…¬å¼è®¡ç®—ç¬¬ n å¤©çš„å¤ªé˜³èµ¤çº¬è§’ï¼ˆå¼§åº¦ï¼‰ï¼Œå…¬å¼ä¸ºï¼š  
  $\delta = 23.44 \times \frac{\pi}{180} \times \sin\left(\frac{2\pi}{365} \times (n - 81)\right)$ã€‚
  å…¶ä¸­81ä¸ºæ˜¥åˆ†æ—¥çš„å¤©æ•°åç§»é‡ï¼ˆ1æœˆ1æ—¥ä¸ºç¬¬1å¤©ï¼‰ã€‚ä¸€èˆ¬æ˜¥åˆ†æ—¥çš„åç§»é‡å…¶å®æ˜¯79ï¼Œä½†æ˜¯ç”±äºåœ°çƒçš„è½¨é“æ˜¯æ¤­åœ†å½¢çš„ï¼Œæ˜¥åˆ†-ç§‹åˆ†æ—¶é•¿ä¸ç§‹åˆ†-æ˜¥åˆ†æ—¶é•¿å¹¶ä¸å®Œå…¨ç›¸ç­‰ï¼Œå®Œå…¨å¯¹é½æ˜¥åˆ†æ—¥çš„åç§»é‡ä¼šå¯¼è‡´ç§‹å­£è¯¯å·®è¾ƒå¤§ï¼Œå› æ­¤è¿™é‡Œä½¿ç”¨äº†èƒ½å¤Ÿç›¸å¯¹å¹³è¡¡æ˜¥ç§‹ä¸¤å­£çš„åç§»é‡81ã€‚

- `compute_day_length (double latitude_rad, int n)`   
  æ ¹æ®è¾“å…¥çš„çº¬åº¦ï¼ˆå¼§åº¦ï¼‰å’Œå¤©æ•°ï¼Œè®¡ç®—å½“å¤©çš„æ—¥ç…§æ—¶é•¿ï¼ˆå•ä½ï¼šå°æ—¶ï¼‰ã€‚  
  ä½¿ç”¨å…¬å¼ $X = -\tan(\phi)\tan(\delta)$ è®¡ç®— X å€¼ï¼Œå…¶ä¸­ $\phi$ ä¸ºçº¬åº¦ï¼ˆå¼§åº¦ï¼‰ã€$\delta$ ä¸ºå¤ªé˜³èµ¤çº¬è§’ï¼ˆå¼§åº¦ï¼‰ã€‚  
  æ­¤å¤„ X è¡¨ç¤ºå¤ªé˜³åœ¨åœ°å¹³é¢ä¸Šå‡é™æ—¶åˆ»å¯¹åº”çš„ä½™å¼¦å€¼ï¼Œå³ $\cos(\omega_0)$ï¼Œé€šè¿‡ X çš„å–å€¼åˆ¤æ–­ï¼š  
  - è‹¥ $X$ åœ¨ $[-1, 1]$ å†…ï¼Œåˆ™é€šè¿‡ $\omega_0 = \arccos(X)$ è®¡ç®—å‡ºå¤ªé˜³çš„å°æ—¶è§’ï¼Œå†åˆ©ç”¨è¯¥è§’åº¦è®¡ç®—æ—¥ç…§æ—¶é•¿ï¼›  
  - è‹¥ $X < -1$ï¼Œåˆ™è¡¨ç¤ºå¤„äºææ˜¼çŠ¶æ€ï¼ˆæ—¥ç…§ 24 å°æ—¶ï¼‰ï¼›  
  - è‹¥ $X > 1$ï¼Œåˆ™è¡¨ç¤ºå¤„äºæå¤œçŠ¶æ€ï¼ˆæ—¥ç…§ 0 å°æ—¶ï¼‰ã€‚

- `generate_day_lengths (double latitude_rad, int year)`  
  éå†ä¸€å¹´ä¸­çš„æ¯ä¸€å¤©ï¼Œè°ƒç”¨ `compute_day_length` è®¡ç®—å„å¤©æ—¥ç…§æ—¶é•¿ï¼Œè¿”å›åŒ…å«æ‰€æœ‰ç™½æ˜¼æ—¶é•¿æ•°æ®çš„æ•°ç»„ã€‚

### ä¸»çª—å£ç±» `DayLengthWindow`
æ­¤ç±»ç»§æ‰¿è‡ª `Gtk.ApplicationWindow`ï¼Œæ˜¯åº”ç”¨ç¨‹åºçš„é¡¶å±‚çª—å£ï¼Œè´Ÿè´£æ„å»ºå›¾å½¢ç•Œé¢å’Œæ˜¾ç¤ºç»˜å›¾å†…å®¹ã€‚`Gtk.ApplicationWindow` æ˜¯ä¸ `Gtk.Application` é›†æˆè‰¯å¥½çš„çª—å£ç±»å‹ï¼Œèƒ½æ›´å¥½åœ°ç®¡ç†åº”ç”¨çš„ç”Ÿå‘½å‘¨æœŸã€‚

- **ç•Œé¢ç»„ä»¶**
  - **å¸ƒå±€å®¹å™¨ `Gtk.Box`**ï¼šGTKä¸­æœ€å¸¸ç”¨çš„å¸ƒå±€å®¹å™¨ä¹‹ä¸€ã€‚ç¬”è€…ä½¿ç”¨äº†ä¸€ä¸ªå‚ç›´æ–¹å‘çš„ `Gtk.Box` (`vbox_main`) ä½œä¸ºä¸»å®¹å™¨ï¼Œå¹¶åœ¨å…¶å†…éƒ¨æ”¾ç½®äº†ä¸€ä¸ªæ°´å¹³æ–¹å‘çš„ `Gtk.Box` (`hbox_controls`) ç”¨äºæ’åˆ—è¾“å…¥æ§ä»¶ã€‚`Gtk.Box` é€šè¿‡ `orientation` å±æ€§ï¼ˆ`Gtk.Orientation.VERTICAL` æˆ– `Gtk.Orientation.HORIZONTAL`ï¼‰å’Œ `spacing` å±æ€§æ¥æ§åˆ¶å­æ§ä»¶çš„æ’åˆ—æ–¹å¼å’Œé—´è·ï¼Œéå¸¸é€‚åˆçº¿æ€§çš„å¸ƒå±€éœ€æ±‚ã€‚
  - **æ–‡æœ¬è¾“å…¥ `Gtk.Entry`**ï¼š`latitude_entry` å’Œ `year_entry` ä½¿ç”¨ `Gtk.Entry` æ§ä»¶ï¼Œå…è®¸ç”¨æˆ·è¾“å…¥å•è¡Œæ–‡æœ¬ã€‚é€šè¿‡è®¾ç½® `input_purpose` å±æ€§ï¼ˆå¦‚ `Gtk.InputPurpose.NUMBER` æˆ– `Gtk.InputPurpose.DIGITS`ï¼‰ï¼Œå¯ä»¥å‘è¾“å…¥æ³•æç¤ºæœŸæœ›çš„è¾“å…¥ç±»å‹ï¼Œæ”¹å–„è§¦æ‘¸é”®ç›˜ä¸Šçš„ç”¨æˆ·ä½“éªŒã€‚`width_chars` å±æ€§åˆ™å¯ä»¥ç²—ç•¥åœ°æ ¹æ®å­—ç¬¦æ•°é‡è®¾å®šè¾“å…¥æ¡†çš„åˆå§‹å®½åº¦ã€‚
  - **æŒ‰é’® `Gtk.Button`**ï¼šâ€œPlotâ€å’Œâ€œExportâ€æŒ‰é’®å‡ä½¿ç”¨ `Gtk.Button.with_label()` æ„é€ ï¼Œè¿™æ˜¯åˆ›å»ºå¸¦æ–‡æœ¬æ ‡ç­¾æŒ‰é’®çš„ä¾¿æ·æ–¹æ³•ã€‚æŒ‰é’®çš„æ ¸å¿ƒåŠŸèƒ½é€šè¿‡è¿æ¥å…¶ `clicked` ä¿¡å·æ¥å®ç°ã€‚
  - **æ ‡ç­¾ `Gtk.Label`**ï¼šç”¨äºæ˜¾ç¤ºæè¿°æ€§æ–‡æœ¬ã€‚é€šè¿‡è®¾ç½® `use_markup = true`ï¼Œå¯ä»¥ä½¿ç”¨ Pango æ ‡è®°è¯­è¨€è¿›è¡Œç®€å•çš„æ–‡æœ¬æ ¼å¼åŒ–ï¼Œä¾‹å¦‚æœ¬ä¾‹ä¸­çš„ `<b>Latitude (degrees):</b>` ä½¿æ–‡æœ¬åŠ ç²—ã€‚`halign = Gtk.Align.START` ç¡®ä¿æ ‡ç­¾æ–‡æœ¬å·¦å¯¹é½ã€‚
  - **ç»˜å›¾åŒºåŸŸ `Gtk.DrawingArea`**ï¼š`drawing_area` æ˜¯ä¸€ä¸ªç©ºç™½çš„ç”»å¸ƒæ§ä»¶ï¼Œä¸“é—¨ç”¨äºè‡ªå®šä¹‰ç»˜å›¾ã€‚é€šè¿‡ `set_draw_func()` æ–¹æ³•æ³¨å†Œä¸€ä¸ªç»˜å›¾å›è°ƒå‡½æ•° (`draw_plot`)ï¼Œå½“æ§ä»¶éœ€è¦é‡ç»˜æ—¶ï¼ˆä¾‹å¦‚çª—å£å¤§å°æ”¹å˜æˆ–è°ƒç”¨ `queue_draw()`ï¼‰ï¼Œè¯¥å‡½æ•°ä¼šè¢«è°ƒç”¨ï¼Œå¹¶ä¼ å…¥ä¸€ä¸ª `Cairo.Context` å¯¹è±¡ç”¨äºç»˜å›¾ã€‚è®¾ç½® `hexpand = true` å’Œ `vexpand = true` ä½¿ç»˜å›¾åŒºåŸŸèƒ½å¤Ÿå¡«å……çˆ¶å®¹å™¨çš„é¢å¤–ç©ºé—´ã€‚

- **äº‹ä»¶å¤„ç†**
  - â€œPlotâ€ æŒ‰é’®çš„ `clicked` ä¿¡å·è¿æ¥åˆ°ä¸€ä¸ªåŒ¿åå‡½æ•°ï¼Œè¯¥å‡½æ•°è°ƒç”¨ `update_plot_data()` è¯»å–è¾“å…¥ã€æ›´æ–°æ•°æ®ï¼Œç„¶åé€šè¿‡ `drawing_area.queue_draw()` è¯·æ±‚é‡ç»˜å›¾è¡¨ã€‚`queue_draw()` ä¼šæ ‡è®° `Gtk.DrawingArea` ä¸ºâ€œè„â€ï¼Œå¹¶åœ¨ä¸‹ä¸€ä¸ªåˆé€‚çš„æ—¶æœºè§¦å‘å…¶ç»˜å›¾å‡½æ•°ã€‚
  - â€œExportâ€ æŒ‰é’®çš„ `clicked` ä¿¡å·ä¹Ÿè¿æ¥åˆ°ä¸€ä¸ªåŒ¿åå‡½æ•°ï¼Œè¯¥å‡½æ•°è´Ÿè´£å¼¹å‡ºæ–‡ä»¶ä¿å­˜å¯¹è¯æ¡†ã€‚
    - **æ–‡ä»¶å¯¹è¯æ¡† `Gtk.FileDialog`**ï¼šè¿™æ˜¯ GTK4 ä¸­ç”¨äºæ–‡ä»¶é€‰æ‹©çš„ç°ä»£åŒ–APIã€‚ç¬”è€…åˆ›å»ºä¸€ä¸ª `Gtk.FileDialog` å¯¹è±¡ï¼Œè®¾ç½®å…¶ä¸º `modal = true` ä½¿å…¶æˆä¸ºæ¨¡æ€å¯¹è¯æ¡†ã€‚é€šè¿‡ `initial_name` è®¾ç½®é»˜è®¤æ–‡ä»¶åã€‚
    - **æ–‡ä»¶è¿‡æ»¤å™¨ `Gtk.FileFilter`**ï¼šä¸ºäº†é™åˆ¶ç”¨æˆ·å¯é€‰çš„æ–‡ä»¶ç±»å‹ï¼Œç¬”è€…åˆ›å»ºäº† `Gtk.FileFilter` å®ä¾‹ï¼Œå¹¶é€šè¿‡ `add_mime_type()` æŒ‡å®šæ”¯æŒçš„MIMEç±»å‹ï¼ˆå¦‚ "image/png", "image/svg+xml", "application/pdf"ï¼‰ã€‚è¿™äº›è¿‡æ»¤å™¨è¢«æ·»åŠ åˆ°ä¸€ä¸ª `ListStore` ä¸­ï¼Œå¹¶èµ‹ç»™ `Gtk.FileDialog` çš„ `filters` å±æ€§ã€‚
    - **å¼‚æ­¥æ“ä½œ**ï¼šæ–‡ä»¶ä¿å­˜æ“ä½œé€šè¿‡ `file_dialog.save.begin()` å’Œ `file_dialog.save.end()` ä»¥å¼‚æ­¥æ–¹å¼æ‰§è¡Œï¼Œé¿å…é˜»å¡ä¸»çº¿ç¨‹ã€‚å›è°ƒå‡½æ•°åœ¨æ“ä½œå®Œæˆï¼ˆæˆ–å–æ¶ˆï¼‰æ—¶è¢«è°ƒç”¨ï¼Œé€šè¿‡ `file_dialog.save.end(res)` è·å–é€‰æ‹©çš„ `File` å¯¹è±¡ã€‚
    - å¯¼å‡ºæ—¶ï¼Œæ ¹æ®æ–‡ä»¶æ‰©å±•åé€‰æ‹©åˆ›å»º `Cairo.ImageSurface` (PNG)ã€`Cairo.SvgSurface` (SVG) æˆ– `Cairo.PdfSurface` (PDF) ä½œä¸ºç»˜å›¾è¡¨é¢ï¼Œç„¶åè°ƒç”¨ `draw_plot()` åœ¨è¿™ä¸ªè¡¨é¢ä¸Šç»˜åˆ¶å›¾è¡¨ï¼Œæœ€åå®Œæˆæ–‡ä»¶å†™å…¥ã€‚

- **ç»˜å›¾æ“ä½œ**  
  - `drawing_area` ä½¿ç”¨ `Gtk.DrawingArea`ï¼Œå¹¶æ³¨å†Œäº†ç»˜å›¾å›è°ƒ `draw_plot ()`ã€‚  
  - `draw_plot ()` ä¸­åˆ©ç”¨ `Cairo` åº“å®Œæˆä»¥ä¸‹å·¥ä½œï¼š  
    * æ¸…ç©ºèƒŒæ™¯å¹¶è®¾ç½®ä¸ºç™½è‰²ã€‚  
    * å®šä¹‰ç»˜å›¾åŒºåŸŸçš„è¾¹è·ã€X/Y è½´èŒƒå›´ã€‚  
    * ç»˜åˆ¶æ¨ªå‘å’Œçºµå‘çš„ç½‘æ ¼çº¿ã€åˆ»åº¦åŠæ–‡å­—æ ‡ç­¾ï¼ˆåŒ…æ‹¬æœˆä»½å’Œå°æ—¶åˆ»åº¦ï¼‰ã€‚  
    * ç»˜åˆ¶åæ ‡è½´ï¼Œå…¶ä¸­ X/Y è½´å‡åŠ ç²—æ˜¾ç¤ºã€‚  
    * åˆ©ç”¨è®¡ç®—å¾—åˆ°çš„ `day_lengths` æ•°æ®ç»˜åˆ¶çº¢è‰²çš„æ—¥ç…§æ—¶é•¿æ›²çº¿ã€‚  
    * é¢å¤–ç»˜åˆ¶åæ ‡è½´æ ‡é¢˜ï¼Œå…¶ä¸­ Y è½´æ–‡å­—é€šè¿‡æ—‹è½¬æ“ä½œå®ç°å‚ç›´æ˜¾ç¤ºã€‚

### åº”ç”¨ç®¡ç†ç±» `DayLengthApp` åŠ `main` å‡½æ•°
- **`DayLengthApp` ç±»**
  ç»§æ‰¿è‡ª `Gtk.Application`ï¼Œè¿™æ˜¯ç®¡ç† GTK åº”ç”¨ç¨‹åºç”Ÿå‘½å‘¨æœŸçš„æ ¸å¿ƒç±»ã€‚å®ƒè´Ÿè´£å¤„ç†åº”ç”¨çš„åˆå§‹åŒ–ã€æ¿€æ´»ã€å‘½ä»¤è¡Œå‚æ•°ä»¥åŠç¡®ä¿åº”ç”¨åªæœ‰ä¸€ä¸ªå®ä¾‹åœ¨è¿è¡Œï¼ˆé€šè¿‡ `application_id`ï¼‰ã€‚
- **`activate()` å›è°ƒå‡½æ•°**
  å½“åº”ç”¨è¢«æ¿€æ´»æ—¶ï¼ˆé€šå¸¸æ˜¯é¦–æ¬¡å¯åŠ¨æˆ–ç”¨æˆ·å°è¯•å¯åŠ¨å·²è¿è¡Œçš„åº”ç”¨å®ä¾‹æ—¶ï¼‰ï¼Œ`activate` æ–¹æ³•ä¼šè¢«è°ƒç”¨ã€‚åœ¨è¿™é‡Œï¼Œç¬”è€…åˆ›å»ºä¸»çª—å£ `DayLengthWindow` çš„å®ä¾‹ï¼Œå¹¶é€šè¿‡ `win.present()`å°†å…¶æ˜¾ç¤ºç»™ç”¨æˆ·ã€‚
- **`main()` å‡½æ•°**
  ç¨‹åºçš„å…¥å£ï¼Œåˆ›å»º `DayLengthApp` å¯¹è±¡å¹¶å¯åŠ¨äº‹ä»¶å¾ªç¯ï¼Œå¤„ç†å‘½ä»¤è¡Œå‚æ•°ã€‚

## å®ç°ä»£ç 

å®ç°è¿™ä¸ªåº”ç”¨ç¨‹åºçš„ä»£ç å¦‚ä¸‹ï¼š

```vala
#!/usr/bin/env -S vala --pkg=gtk4 -X -lm -X -O2 -X -march=native

// Helper functions to compute day-of-year, solar declination and day length

/**
 * Returns the number of days in a given year.
 *
 * @param year The year to calculate the number of days.
 * @return Total number of days in the year.
 */
private inline int days_in_year (int year) {
    // Leap year: divisible by 400 or divisible by 4 but not by 100.
    if ((year % 4 == 0) && ((year % 100 != 0) || (year % 400 == 0))) {
        return 366;
    }
    return 365;
}

/**
 * Computes solar declination in radians using an approximate formula.
 *
 * Formula: Î´ (rad) = (23.44 * Ï€/180) * sin(2Ï€/365 * (n - 81))
 *
 * @param n The day number in the year.
 * @return Solar declination in radians.
 */
private inline double solar_declination (int n) {
    return (23.44 * Math.PI / 180.0) * Math.sin (2 * Math.PI / 365.0 * (n - 81));
}

/**
 * Calculates the day length (in hours) for a given latitude (in radians) and day number.
 *
 * Using formula: T = (24/Ï€) * arccos( -tan(Ï†) * tan(Î´) )
 *
 * Ï†: observer's latitude, Î´: solar declination
 *
 * When |tan Ï† * tan Î´| > 1, returns polar day (24 hours) or polar night (0 hours)
 *
 * @param latitude_rad Latitude in radians.
 * @param n The day number in the year.
 * @return Day length in hours.
 */
private inline double compute_day_length (double latitude_rad, int n) {
    double phi = latitude_rad;
    double delta = solar_declination (n);
    double X = -Math.tan (phi) * Math.tan (delta);
    if (X < -1) {
        return 24.0; // Polar day
    } else if (X > 1) {
        return 0.0;  // Polar night
    } else {
        // 'omega0' is the half-angle (in radians) corresponding to the time from sunrise to solar noon.
        // Since 2Ï€ radians represent 24 hours, 1 radian equals 24/(2Ï€) hours.
        // Multiplying omega0 by (24/Math.PI) converts this angle to the total day length in hours.
        double omega0 = Math.acos (X); // computed in radians
        double T = (24.0 / Math.PI) * omega0; // convert to hours
        return T;
    }
}

/**
 * Generates an array of day lengths for all days at the given latitude (in radians) and year.
 *
 * @param latitude_rad Latitude in radians.
 * @param year The year for which to generate day lengths.
 * @return Array of day lengths in hours.
 */
private inline double[] generate_day_lengths (double latitude_rad, int year) {
    int total_days = days_in_year (year);
    double[] lengths = new double[total_days];
    for (int i = 0; i < total_days; i += 1) {
        lengths[i] = compute_day_length (latitude_rad, i + 1);
    }
    return lengths;
}

/**
 * Window for displaying the day length plot.
 */
public class DayLengthWindow : Gtk.ApplicationWindow {
    private Gtk.Entry latitude_entry;
    private Gtk.Entry year_entry;
    private Gtk.DrawingArea drawing_area;
    private double[] day_lengths;
    private int current_year;
    private double latitude_deg;
    private Gtk.Button export_button;

    /**
     * Constructs a new DayLengthWindow.
     *
     * @param app The Gtk.Application instance.
     */
    public DayLengthWindow (Gtk.Application app) {
        Object (
            application: app,
            title: "Day Length Plotter",
            default_width: 800,
            default_height: 600
        );

        // Initialize current_year first
        DateTime now = new DateTime.now_local ();
        current_year = now.get_year ();

        // Use vertical box as the main container
        var vbox_main = new Gtk.Box (Gtk.Orientation.VERTICAL, 10) {
            // Add top margin but do not add other margins since drawing area expands
            margin_top = 10
        };
        this.child = vbox_main;

        // Input area (using horizontal Box layout)
        var hbox_controls = new Gtk.Box (Gtk.Orientation.HORIZONTAL, 10) {
            margin_start = 10,
            margin_end = 10
        };
        vbox_main.append (hbox_controls);

        var lat_label = new Gtk.Label ("<b>Latitude (degrees):</b>") {
            halign = Gtk.Align.START,
            use_markup = true
        };
        hbox_controls.append (lat_label);

        latitude_entry = new Gtk.Entry () {
            width_chars = 10,
            input_purpose = Gtk.InputPurpose.NUMBER
        };
        hbox_controls.append (latitude_entry);

        var year_label = new Gtk.Label ("<b>Year:</b>") {
            halign = Gtk.Align.START,
            use_markup = true
        };
        hbox_controls.append (year_label);

        year_entry = new Gtk.Entry () {
            width_chars = 10,
            input_purpose = Gtk.InputPurpose.DIGITS,
            // Set year entry text using current_year
            text = current_year.to_string ()
        };
        hbox_controls.append (year_entry);

        var plot_button = new Gtk.Button.with_label ("Plot");
        hbox_controls.append (plot_button);
        plot_button.clicked.connect (() => {
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        // Export image button
        export_button = new Gtk.Button.with_label ("Export");
        hbox_controls.append (export_button);
        export_button.clicked.connect (() => {
            var png_filter = new Gtk.FileFilter ();
            png_filter.name = "PNG Images";
            png_filter.add_mime_type ("image/png");
            
            var svg_filter = new Gtk.FileFilter ();
            svg_filter.name = "SVG Images";
            svg_filter.add_mime_type ("image/svg+xml");

            var pdf_filter = new Gtk.FileFilter ();
            pdf_filter.name = "PDF Documents";
            pdf_filter.add_mime_type ("application/pdf");

            // FileDialog.filters are required to contain default filter and others
            var filter_list = new ListStore (typeof (Gtk.FileFilter));
            filter_list.append (png_filter);
            filter_list.append (svg_filter);
            filter_list.append (pdf_filter);

            var file_dialog = new Gtk.FileDialog () {
                modal = true,
                initial_name = "daylength_plot.png",
                filters = filter_list
            };

            file_dialog.save.begin (this, null, (obj, res) => {
                try {
                    var file = file_dialog.save.end (res);
                    if (file != null) {
                        string filepath = file.get_path ();
                        export_plot (filepath);
                    }
                } catch (Error e) {
                    // Usually due to the user canceling the dialog
                    message ("File has not been saved: %s", e.message);
                }
            });
        });

        // Drawing area: using Gtk.DrawingArea and Cairo for plotting
        drawing_area = new Gtk.DrawingArea () {
            hexpand = true,
            vexpand = true
        };
        drawing_area.set_size_request (400, 400);
        drawing_area.set_draw_func (this.draw_plot);
        vbox_main.append (drawing_area);
    }

    /**
     * Updates plot data based on input values.
     */
    private void update_plot_data () {
        latitude_deg = double.parse (latitude_entry.text);
        current_year = int.parse (year_entry.text);
        // Convert input latitude (in degrees) to radians
        double latitude_rad = latitude_deg * Math.PI / 180.0;
        day_lengths = generate_day_lengths (latitude_rad, current_year);
    }

    /**
     * Drawing callback to render the day length plot.
     *
     * @param area The drawing area widget.
     * @param cr The Cairo context.
     * @param width The width of the drawing area.
     * @param height The height of the drawing area.
     */
    private void draw_plot (Gtk.DrawingArea area, Cairo.Context cr, int width, int height) {
        // Clear background to white
        cr.set_source_rgb (1, 1, 1);
        cr.paint ();

        // Set margins
        int margin_left = 75;
        int margin_right = 20;
        int margin_top = 50;
        int margin_bottom = 70;
        int plot_width = width - margin_left - margin_right;
        int plot_height = height - margin_top - margin_bottom;

        // Fixed Y axis range: -0.5 to 24.5
        double y_min = -0.5, y_max = 24.5;
        // X axis range: 1 to total_days
        int total_days = (day_lengths != null) ? day_lengths.length : 365;

        // Draw grid lines (gray)
        cr.set_source_rgba (0.5, 0.5, 0.5, 0.5);
        cr.set_line_width (1.0);
        // Horizontal grid lines (every 3 hours)
        for (int tick = 0; tick <= 24; tick += 3) {
            double y_val = margin_top + (plot_height * (1 - (tick - y_min) / (y_max - y_min)));
            cr.move_to (margin_left, y_val);
            cr.line_to (width - margin_right, y_val);
            cr.stroke ();
        }
        // Vertical grid lines (start of each month)
        for (int month = 1; month <= 12; month += 1) {
            DateTime month_start = new DateTime (new TimeZone.local (), current_year, month, 1, 0, 0, 0);
            int day_num = month_start.get_day_of_year ();
            double x_pos = margin_left + (plot_width * ((double) (day_num - 1) / (total_days - 1)));
            cr.move_to (x_pos, margin_top);
            cr.line_to (x_pos, height - margin_bottom);
            cr.stroke ();
        }

        // Draw axes (black, bold)
        cr.set_source_rgb (0, 0, 0);
        cr.set_line_width (2.0);
        // X axis
        cr.move_to (margin_left, height - margin_bottom);
        cr.line_to (width - margin_right, height - margin_bottom);
        cr.stroke ();
        // Y axis
        cr.move_to (margin_left, margin_top);
        cr.line_to (margin_left, height - margin_bottom);
        cr.stroke ();

        // Draw Y axis ticks
        cr.set_line_width (1.0);
        for (int tick = 0; tick <= 24; tick += 3) {
            double y_val = margin_top + (plot_height * (1 - (tick - y_min) / (y_max - y_min)));
            cr.move_to (margin_left - 5, y_val);
            cr.line_to (margin_left, y_val);
            cr.stroke ();
            // Draw tick labels
            cr.set_font_size (22);
            Cairo.TextExtents ext;
            cr.text_extents (tick.to_string (), out ext);
            cr.move_to (margin_left - 10 - ext.width, y_val + ext.height / 2);
            cr.show_text (tick.to_string ());
        }

        // Draw X axis ticks (start of each month)
        for (int month = 1; month <= 12; month += 1) {
            // Use GLib.DateTime to construct the 1st of each month
            DateTime month_start = new DateTime (new TimeZone.local (), current_year, month, 1, 0, 0, 0);
            int day_num = month_start.get_day_of_year ();
            double x_pos = margin_left + (plot_width * ((double) (day_num - 1) / (total_days - 1)));
            cr.move_to (x_pos, height - margin_bottom);
            cr.line_to (x_pos, height - margin_bottom + 5);
            cr.stroke ();
            // Draw month labels
            string label = month.to_string ();
            cr.set_font_size (22);
            Cairo.TextExtents ext;
            cr.text_extents (label, out ext);
            cr.move_to (x_pos - ext.width / 2, height - margin_bottom + 20);
            cr.show_text (label);
        }

        // Draw X and Y axis titles
        cr.set_source_rgb (0, 0, 0);
        cr.set_font_size (22);

        // X axis title
        string x_title = "Date (Month)";
        Cairo.TextExtents x_ext;
        cr.text_extents (x_title, out x_ext);
        cr.move_to ((double) width / 2 - x_ext.width / 2, height - margin_bottom + 50);
        cr.show_text (x_title);

        // Y axis title
        string y_title = "Day Length (hours)";
        Cairo.TextExtents y_ext;
        cr.text_extents (y_title, out y_ext);
        cr.save ();
        // Position 45 pixels to the left of Y axis, vertically centered
        cr.translate (margin_left - 45, (double) height / 2);
        // Rotate -90 degrees (Ï€/2) for vertical text
        cr.rotate (-Math.PI / 2);
        // Adjust text position for vertical centering
        cr.move_to (-y_ext.width / 2, 0);
        cr.show_text (y_title);
        cr.restore ();

        // Add caption below the X axis title for clarity and aesthetics
        string caption;
        if (day_lengths == null) {
            caption = "Day Length";
        } else {
            caption = "Day Length - Latitude: %.2fÂ°, Year: %d".printf (latitude_deg, current_year);
        }
        cr.set_font_size (22);
        Cairo.TextExtents cap_ext;
        cr.text_extents (caption, out cap_ext);
        cr.move_to ((width - cap_ext.width) / 2, (double) margin_top / 2);
        cr.show_text (caption);

        // Return if no data
        if (day_lengths == null) {
            return;
        }

        // Draw data curve (red, bold)
        cr.set_source_rgb (1, 0, 0);
        cr.set_line_width (2.5);
        for (int i = 0; i < total_days; i += 1) {
            double x = margin_left + (plot_width * ((double) i / (total_days - 1)));
            double y = margin_top + (plot_height * (1 - (day_lengths[i] - y_min) / (y_max - y_min)));
            if (i == 0) {
                cr.move_to (x, y);
            } else {
                cr.line_to (x, y);
            }
        }
        cr.stroke ();
    }

    /**
     * Exports the current day length plot to an image file (PNG or SVG).
     *
     * This function gets the current width and height of the drawing area.
     * If invalid, it defaults to 800x600.
     * It then creates a Cairo surface (SVG or PNG) and draws the plot onto it.
     *
     * @param filepath The destination file path.
     */
    private void export_plot (string filepath) {
        int width = drawing_area.get_width ();
        int height = drawing_area.get_height ();

        if (width <= 0 || height <= 0) {
            width = 800;
            height = 600;
        }

        string? extension = null;
        var last_dot = filepath.last_index_of_char ('.');
        if (last_dot != -1) {
            extension = filepath[last_dot:].down ();
        }

        if (extension == ".svg") {
            Cairo.SvgSurface surface = new Cairo.SvgSurface (filepath, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_plot (drawing_area, cr, width, height);
        } else if (extension == ".pdf") {
            Cairo.PdfSurface surface = new Cairo.PdfSurface (filepath, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_plot (drawing_area, cr, width, height);
        } else {
            Cairo.ImageSurface surface = new Cairo.ImageSurface (Cairo.Format.RGB24, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_plot (drawing_area, cr, width, height);
            surface.write_to_png (filepath);
        }
    }
}

/**
 * Main application class for Day Length Plotter.
 */
public class DayLengthApp : Gtk.Application {

    /**
     * Constructs a new DayLengthApp.
     */
    public DayLengthApp () {
        Object (
            application_id: "com.github.wszqkzqk.DayLengthApp",
            flags: ApplicationFlags.DEFAULT_FLAGS
        );
    }

    /**
     * Activates the application.
     */
    protected override void activate () {
        var win = new DayLengthWindow (this);
        win.present ();
    }
}

/**
 * Main entry point.
 *
 * @param args Command line arguments.
 * @return Exit status code.
 */
public static int main (string[] args) {
    var app = new DayLengthApp ();
    return app.run (args);
}
```

## ç¼–è¯‘ä¸è¿è¡Œè¯´æ˜
- æ–‡ä»¶æœ€ä¸Šæ–¹çš„ shebang æŒ‡å®šäº†ç¼–è¯‘å‘½ä»¤ï¼Œå…¶åŒ…å«äº†ï¼š
  - æŒ‡å®š Vala ç¼–è¯‘å™¨ï¼ˆ`#!/usr/bin/env -S vala`ï¼‰ã€‚
  - æ·»åŠ  gtk4 åŒ…ï¼ˆ`--pkg=gtk4`ï¼‰ã€‚
  - é“¾æ¥æ•°å­¦åº“ (`-lm`)ã€‚
  - å„ç§ç¼–è¯‘ä¼˜åŒ–å‚æ•°ï¼ˆå¦‚ `-O2`ã€`-march=native` ç­‰ï¼‰ã€‚
- è¿è¡Œå‰è¯·ç¡®ä¿ç³»ç»Ÿå·²å®‰è£… Valaã€GTK4 ä»¥åŠç›¸å…³ç¼–è¯‘å·¥å…·ï¼ˆå¦‚ `gcc`ï¼‰ã€‚
  - åœ¨ Arch Linux ä¸Šï¼Œå¯ä»¥ä½¿ç”¨ä»¥ä¸‹å‘½ä»¤å®‰è£…æ‰€éœ€çš„åŒ…ï¼š
    ```bash
    sudo pacman -S --needed vala gtk4
    ```
  - Windows ç”¨æˆ·å¯ä»¥ä½¿ç”¨ MSYS2 ç¯å¢ƒï¼Œå®‰è£…ç›¸å…³åŒ…ï¼š
    ```bash
    pacman -S --needed mingw-w64-ucrt-x86_64-vala mingw-w64-ucrt-x86_64-gcc mingw-w64-ucrt-x86_64-gtk4
    ```
- å¯ä»¥ç›´æ¥æ‰§è¡Œè¯¥è„šæœ¬ï¼Œä¹Ÿå¯ä»¥åœ¨å‘½ä»¤è¡Œä¸­ä½¿ç”¨å¦‚ä¸‹å‘½ä»¤è¿›è¡Œç¼–è¯‘ï¼š
  ```bash
  valac --pkg gtk4 -X -lm -X -pipe -X -O2 daylengthgtk.vala
  ```

# GTK4åº”ç”¨ï¼šå¤ªé˜³é«˜åº¦è§’è®¡ç®—ä¸ç»˜åˆ¶å·¥å…·

åœ¨äº†è§£äº†ç™½æ˜¼æ—¶é•¿è®¡ç®—ç¨‹åºåï¼Œæˆ‘ä»¬å†æ¥çœ‹ä¸€ä¸ªé€»è¾‘ä¸å¸ƒå±€æ›´åŠ å¤æ‚çš„ GTK4 åº”ç”¨ç¨‹åºã€‚è¿™ä¸ªç¨‹åºç”¨äºè®¡ç®—å¹¶ç»˜åˆ¶åœ°çƒä¸Šä»»æ„ä½ç½®ï¼ˆçº¬åº¦ã€ç»åº¦ï¼‰ã€ä»»æ„æ—¥æœŸå’Œæ—¶åŒºä¸‹ï¼Œå¤ªé˜³é«˜åº¦è§’éšä¸€å¤©ä¸­æ—¶é—´å˜åŒ–çš„æ›²çº¿ã€‚å®ƒå°†è¿›ä¸€æ­¥å±•ç¤º GTK4 ä¸­ `Gtk.SpinButton`ã€`Gtk.Calendar`ã€`Gtk.GestureClick` å’Œ `Gtk.Grid` ç­‰ç»„ä»¶çš„ä½¿ç”¨ï¼Œä»¥åŠ Cairo ç»˜å›¾æ›´ç²¾ç»†çš„æ§åˆ¶ï¼Œä¾‹å¦‚ç»˜åˆ¶é˜´å½±åŒºåŸŸã€ç»˜åˆ¶ç‚¹å‡»ä½ç½®çš„æ ‡è®°ç­‰ã€‚

ä¸ºäº†è¾¾åˆ°æ›´é«˜çš„ç²¾åº¦ï¼Œç¨‹åºç°åœ¨ä½¿ç”¨æ›´ç²¾ç¡®çš„[NOAAèµ¤çº¬å…¬å¼](https://gml.noaa.gov/grad/solcalc/solareqns.PDF)æ¥è®¡ç®—å¤ªé˜³èµ¤çº¬è§’ï¼Œè¯¥èµ¤çº¬å…¬å¼é€šè¿‡ä¿ç•™æ›´å¤šå‚…é‡Œå¶çº§æ•°é¡¹æ¥æé«˜ç²¾åº¦ã€‚ç¬”è€…è¿˜æ ¹æ®NOAAçš„è¿™ä¸€æ–‡æ¡£ï¼Œå¼•å…¥äº†å‡æ—¶å·®å’ŒçœŸå¤ªé˜³æ—¶çš„è®¡ç®—ï¼Œä»¥æ›´ç²¾ç¡®åœ°ç¡®å®šå¤ªé˜³çš„å®é™…ä½ç½®ã€‚

è¯¥ç¨‹åºåŒæ ·åšäº†ä¸€äº›ç®€åŒ–ï¼š

* å‚…é‡Œå¶çº§æ•°çš„è®¡ç®—å±•å¼€åˆ° 3 é˜¶
  * ç›¸æ¯”äºç®€å•çš„å‡ ä½•æ¨¡å‹ï¼Œè¿™åŒ…å«äº†æ›´å¤šå¤æ‚çš„å‘¨æœŸæ€§å˜åŒ–
  * ä½†ä»æ— æ³•è€ƒè™‘é•¿æœŸçš„å²å·®ç­‰æ•ˆåº”

å¦å¤–ï¼Œåœ¨ä½¿ç”¨æ—¶éœ€è¦æ³¨æ„ï¼š

* æœ¬ç¨‹åºè®¡ç®—çš„æ˜¯**å®é™…è§’åº¦**è€Œéç›®æµ‹è§’åº¦ï¼ˆæ˜¾ç„¶ç›®æµ‹è§’åº¦æ— æ³•åŒ…æ‹¬åœ°å¹³çº¿ä»¥ä¸‹çš„çŠ¶æ€ï¼‰
  * è§’åº¦æœ¬èº«æ˜¯çœŸå®é«˜åº¦è§’ï¼Œæ²¡æœ‰è®¡å…¥å¤§æ°”æŠ˜å°„å¯¼è‡´çš„è§†è§‰åå·®
  * å¯ä»¥åœ¨å®é™…åº”ç”¨æ—¶åŸºäºåŸå§‹è§’åº¦è‡ªè¡Œå¤„ç†
  * ä¾‹å¦‚ï¼Œè®¡ç®—**æ—¥å‡ºæ—¥è½**æ—¶é—´ï¼Œ**åœ°å¹³çº¿åº”å½“é€‰æ‹© -0.833 åº¦**è€Œä¸æ˜¯ 0 åº¦
    * ç¬”è€…åœ¨æŒ‰ç…§è¯¥æ–¹æ³•è®¡ç®—2025.06.02 - 2025.06.05åŒ—äº¬ä¸é‡åº†çš„æ—¥å‡ºæ—¥è½æ—¶é—´æ—¶ï¼Œå‘ç°ä¸å½“æ—¥å¤©æ°”é¢„æŠ¥ç»™å‡ºçš„å€¼**å®Œå…¨ä¸€è‡´**ï¼ˆç²¾ç¡®åˆ°åˆ†é’Ÿï¼‰

## è¿è¡Œæ•ˆæœ

|[![#~/img/GTK-examples/chongqing-solar-angle-spring.webp](/img/GTK-examples/chongqing-solar-angle-spring.webp)](/img/GTK-examples/chongqing-solar-angle-spring.webp)|[![#~/img/GTK-examples/beijing-solar-angle-summer.webp](/img/GTK-examples/beijing-solar-angle-summer.webp)](/img/GTK-examples/beijing-solar-angle-summer.webp)|
|:----:|:----:|
|é‡åº†(åŒ—åŠçƒæ˜¥åˆ†)|åŒ—äº¬(åŒ—åŠçƒå¤è‡³)|
|[![#~/img/GTK-examples/singapore-solar-angle-autumn.webp](/img/GTK-examples/singapore-solar-angle-autumn.webp)](/img/GTK-examples/singapore-solar-angle-autumn.webp)|[![#~/img/GTK-examples/n-polar-solar-angle-winter.webp](/img/GTK-examples/n-polar-solar-angle-winter.webp)](/img/GTK-examples/n-polar-solar-angle-winter.webp)|
|æ–°åŠ å¡(åŒ—åŠçƒç§‹åˆ†)|åŒ—æç‚¹(åŒ—åŠçƒå†¬è‡³)|

## ç¨‹åºæ¶æ„

## æ ¸å¿ƒè®¡ç®—å‡½æ•°

- `generate_sun_angles (double latitude_rad, int day_of_year, int year, double longitude_deg, double timezone_offset_hrs)`
    - è®¡ç®—æ¯æ—¥æ¯åˆ†é’Ÿçš„å¤ªé˜³é«˜åº¦è§’ã€‚
    - **æ—¥è¡Œè½¨è¿¹ç»„åˆ†ä¸å¹´è§’è®¡ç®—**ï¼š
        - `fractional_day_component = day_of_year - 1 + ((double) i) / RESOLUTION_PER_MIN`ï¼šè®¡ç®—ä¸€å¹´ä¸­çš„å…·ä½“æ—¶åˆ»ï¼ˆä»¥å¤©ä¸ºå•ä½ï¼ŒåŒ…å«å°æ•°éƒ¨åˆ†ï¼‰ã€‚
        - `gamma_rad = (2.0 * Math.PI / days_in_year) * fractional_day_component`ï¼šè®¡ç®—å¹´è§’ï¼ˆå¼§åº¦ï¼‰ï¼Œè¡¨ç¤ºåœ°çƒåœ¨è½¨é“ä¸Šçš„ç²¾ç¡®ä½ç½®ã€‚
    - **å¤ªé˜³èµ¤çº¬ `Î´` è®¡ç®—**ï¼ˆä½¿ç”¨ NOAA å‚…é‡Œå¶çº§æ•°è¿‘ä¼¼å…¬å¼ï¼‰ï¼š
      å°†ä¸Šè¿° `gamma_rad` ä»£å…¥ç»éªŒå…¬å¼ï¼š

      $$
      \begin{aligned}
      \delta &= 0.006918 - 0.399912 \cos(\gamma)
              + 0.070257 \sin(\gamma) \\
          &\quad - 0.006758 \cos(2 \times \gamma)
              + 0.000907 \sin(2 \times \gamma) \\
          &\quad - 0.002697 \cos(3 \times \gamma)
              + 0.001480 \sin(3 \times \gamma)
      \end{aligned}
      $$

    - **å‡æ—¶å·® (Equation of Time, EoT) è®¡ç®—**ï¼š
      `eqtime_minutes = 229.18 * (0.000075 + 0.001868 * cos(gamma_rad) ...)`ï¼šè®¡ç®—å‡æ—¶å·®ï¼ˆåˆ†é’Ÿï¼‰ï¼ŒçœŸå¤ªé˜³æ—¶ï¼ˆTrue Solar Timeï¼ŒåŸºäºå¤ªé˜³çœŸå®ä½ç½®ï¼‰ä¸å‡å¤ªé˜³æ—¶ï¼ˆMean Solar Timeï¼Œå‡è®¾å¤ªé˜³åŒ€é€Ÿè¿è¡Œï¼‰ä¹‹å·®ï¼Œä¸»è¦ç”±åœ°çƒè½¨é“åå¿ƒç‡å’Œé»„èµ¤äº¤è§’å¼•èµ·ï¼Œåæ˜ é’Ÿè¡¨æ—¶é—´å’Œæ—¥æ™·æ—¶é—´çš„åå·®ã€‚å°†æœ¬åœ°å¹³æ—¶ï¼ˆåˆ†é’Ÿ `i`ï¼‰ä¿®æ­£ä¸ºçœŸå¤ªé˜³æ—¶ï¼ˆåˆ†é’Ÿï¼‰ï¼Œä»¥ä¿è¯åç»­æ—¶è§’ã€å¤ªé˜³é«˜åº¦è§’è®¡ç®—çš„å¤©æ–‡ç²¾åº¦ã€‚
        - çœŸå¤ªé˜³æ—¶ï¼šåŸºäºå¤ªé˜³åœ¨å¤©ç©ºä¸­çš„å®é™…ä½ç½®è®¡ç®—ã€‚
        - å¹³å¤ªé˜³æ—¶ï¼šè™šæ„ä¸€ä¸ªåŒ€é€Ÿè¿åŠ¨çš„â€œå¹³å¤ªé˜³â€ä½œä¸ºå‚è€ƒï¼Œå°†ä¸€å¤©å›ºå®šä¸º24å°æ—¶ï¼ˆ86,400ç§’ï¼‰ï¼Œæ¶ˆé™¤å­£èŠ‚æ€§æ³¢åŠ¨ã€‚è¿™æ˜¯æ—¥å¸¸é’Ÿè¡¨æ—¶é—´çš„åŸºå‡†ã€‚
        - çœŸå¤ªé˜³æ—¥çš„é•¿åº¦ï¼ˆå¤ªé˜³è¿ç»­ä¸¤æ¬¡è¿‡ä¸­å¤©çš„æ—¶é—´é—´éš”ï¼‰ä¼šå› åœ°çƒè½¨é“ç¦»å¿ƒç‡å’Œé»„èµ¤äº¤è§’çš„å½±å“è€Œå˜åŒ–ï¼Œå¯è¾¾Â±30ç§’ã€‚
        - è¿™äº›å¾®å°æ—¥å˜åŒ–ä¼šé€æ—¥ç´¯ç§¯ï¼Œå¯¼è‡´çœŸå¤ªé˜³æ—¶ä¸å¹³å¤ªé˜³æ—¶çš„åå·®å¯è¾¾ **-14åˆ†15ç§’è‡³+16åˆ†25ç§’** ï¼ˆå…¬å…ƒ2000å¹´ï¼‰ï¼Œå› æ­¤æœ‰å¿…è¦å‡æ—¶å·®ä¿®æ­£ã€‚
    - **çœŸå¤ªé˜³æ—¶ (True Solar Time, TST) è®¡ç®—**ï¼š  
      `tst_minutes = i + eqtime_minutes + 4.0 * longitude_deg - 60.0 * timezone_offset_hrs`  
      å°†æœ¬åœ°é’Ÿè¡¨æ—¶é—´ï¼ˆåˆ†é’Ÿ `i`ï¼‰å…ˆåŠ ä¸Šå‡æ—¶å·®ä¿®æ­£ï¼ˆ`eqtime_minutes`ï¼‰ï¼Œå†åŠ ä¸Šå› ç»åº¦ï¼ˆæ¯å‘ä¸œ 1 åº¦ +4 åˆ†é’Ÿï¼‰å¸¦æ¥çš„åˆ†é’Ÿåç§»ï¼Œæœ€åå‡å»å› æ—¶åŒºå¸¦æ¥çš„åˆ†é’Ÿå·®ï¼Œå¾—åˆ°çœŸå¤ªé˜³æ—¶ï¼ˆåˆ†é’Ÿï¼‰ã€‚  
      - `longitude_deg`ï¼šç»åº¦ï¼ˆåº¦ï¼‰ï¼Œæ­£å€¼ä¸ºä¸œç»ã€è´Ÿå€¼ä¸ºè¥¿ç»ï¼›  
      - `timezone_offset_hrs`ï¼šæ—¶åŒºåç§»ï¼ˆå°æ—¶ï¼‰ï¼Œæ­£å€¼ä¸ºä¸œåŒºã€è´Ÿå€¼ä¸ºè¥¿åŒºï¼›  
      - `4.0 * longitude_deg`ï¼šå°†ç»åº¦è½¬æ¢ä¸ºåˆ†é’Ÿåç§»ï¼›  
      - `60.0 * timezone_offset_hrs`ï¼šå°†æ—¶åŒºå°æ—¶æ•°è½¬æ¢ä¸ºåˆ†é’Ÿåç§»ï¼›
    - **æ—¶è§’ (Hour Angle, HA) è®¡ç®—**ï¼š
      `ha_deg = (tst_minutes / 4.0) - 180.0`ï¼šæ ¹æ®çœŸå¤ªé˜³æ—¶è®¡ç®—æ—¶è§’ï¼ˆåº¦ï¼‰ï¼Œè¡¨ç¤ºå¤ªé˜³ç›¸å¯¹äºæœ¬åœ°å­åˆçº¿çš„è§’è·ç¦»ã€‚
    - **å¤ªé˜³é«˜åº¦è§’è®¡ç®—**ï¼š
      ä½¿ç”¨çƒé¢ä¸‰è§’å…¬å¼ï¼Œç»“åˆçº¬åº¦ `latitude_rad`ã€å¤ªé˜³èµ¤çº¬ `decl_rad` å’Œæ—¶è§’ `ha_rad` è®¡ç®—å¤ªé˜³å¤©é¡¶è§’ `phi_rad`ï¼Œè¿›è€Œå¾—åˆ°å¤ªé˜³é«˜åº¦è§’ `(90Â° - phi_rad)`ã€‚
    - ç»“æœå¡«å……åˆ° `sun_angles` æ•°ç»„ï¼ˆå•ä½ï¼šÂ°ï¼‰ï¼Œæ¯åˆ†é’Ÿä¸€ä¸ªé‡‡æ ·ç‚¹ã€‚

- `update_plot_data ()`
  - ä» `latitude_spin`ã€`longitude_spin`ã€`timezone_spin` å’Œ `calendar` è¯»å–å‚æ•°ã€‚
  - è°ƒç”¨ `generate_sun_angles` æ›´æ–°å¤ªé˜³é«˜åº¦è§’æ•°æ®ã€‚
  - æ¸…é™¤ä»»ä½•å…ˆå‰åœ¨å›¾è¡¨ä¸Šé€‰ä¸­çš„æ•°æ®ç‚¹ä¿¡æ¯ï¼ˆé‡ç½® `has_click_point` å’Œ `click_info_label` çš„æ–‡æœ¬ï¼‰ã€‚

## ç•Œé¢ä¸äº‹ä»¶å¤„ç†

- **ä¸»å¸ƒå±€**ï¼š
    - `main_box (Gtk.Box)`ï¼šä¾ç„¶ä½¿ç”¨ `Gtk.Box` ä½œä¸ºé¡¶å±‚æ°´å¹³å¸ƒå±€å®¹å™¨ï¼Œå°†ç•Œé¢åˆ†ä¸ºå·¦ä¾§çš„æ§åˆ¶é¢æ¿å’Œå³ä¾§çš„ç»˜å›¾åŒºåŸŸã€‚è¿™ç§åˆ’åˆ†æ–¹å¼æ¸…æ™°ç›´è§‚ã€‚
    - `left_panel (Gtk.Box)`ï¼šå·¦ä¾§æ§åˆ¶é¢æ¿æœ¬èº«æ˜¯ä¸€ä¸ªå‚ç›´æ’åˆ—çš„ `Gtk.Box`ï¼Œç”¨äºç»„ç»‡çº¬åº¦ã€ç»åº¦ã€æ—¶åŒºè®¾ç½®ã€æ—¥æœŸé€‰æ‹©ã€å¯¼å‡ºæŒ‰é’®å’Œç‚¹å‡»ä¿¡æ¯æ˜¾ç¤ºç­‰å¤šä¸ªæ§ä»¶ç»„ã€‚è®¾ç½® `hexpand = false` ç¡®ä¿å·¦ä¾§é¢æ¿ä¸ä¼šåœ¨æ°´å¹³æ–¹å‘ä¸Šæ— é™æ‰©å±•ï¼Œä¿æŒåˆé€‚çš„å®½åº¦ã€‚
- **ä½ç½®å’Œæ—¶é—´è®¾ç½®**ï¼š
    - **ç½‘æ ¼å¸ƒå±€ `Gtk.Grid`**ï¼šå¯¹äºçº¬åº¦ã€ç»åº¦å’Œæ—¶åŒºè¿™ä¸‰ç»„â€œæ ‡ç­¾ + è¾“å…¥æ¡†â€çš„å¸ƒå±€ï¼Œç¬”è€…é€‰æ‹©äº† `Gtk.Grid` (`settings_grid`)ã€‚ç›¸æ¯”äºåµŒå¥—çš„ `Gtk.Box`ï¼Œ`Gtk.Grid` æä¾›äº†æ›´å¼ºå¤§å’Œçµæ´»çš„äºŒç»´å¯¹é½æ–¹å¼ã€‚é€šè¿‡ `attach(child, column, row, width, height)` æ–¹æ³•ï¼Œå¯ä»¥å°†å­æ§ä»¶ç²¾ç¡®åœ°æ”¾ç½®åœ¨ç½‘æ ¼çš„æŒ‡å®šå•å…ƒæ ¼ä¸­ï¼Œå¹¶å¯è·¨è¶Šå¤šè¡Œæˆ–å¤šåˆ—ï¼ˆè¿™é‡Œä»…ä½¿ç”¨äº†å•è¡Œå•åˆ—ï¼‰ã€‚`column_spacing` å’Œ `row_spacing` å±æ€§ç”¨äºè®¾ç½®å•å…ƒæ ¼ä¹‹é—´çš„é—´è·ï¼Œä½¿å¸ƒå±€æ›´åŠ è§„æ•´ç¾è§‚ã€‚
    - **æ•°å­—è¾“å…¥ `Gtk.SpinButton`**ï¼šçº¬åº¦ã€ç»åº¦å’Œæ—¶åŒºå‡ä½¿ç”¨ `Gtk.SpinButton` è¿›è¡Œè¾“å…¥ã€‚`Gtk.SpinButton` ä¸“é—¨ç”¨äºè¾“å…¥æœ‰èŒƒå›´é™åˆ¶çš„æ•°å€¼ï¼Œç”¨æˆ·å¯ä»¥é€šè¿‡ç‚¹å‡»ä¸Šä¸‹ç®­å¤´æˆ–ç›´æ¥è¾“å…¥æ¥æ”¹å˜å€¼ã€‚
        - `Gtk.SpinButton.with_range(min, max, step)` æ˜¯å…¶å¸¸ç”¨çš„æ„é€ å‡½æ•°ï¼Œåˆ†åˆ«è®¾ç½®æœ€å°å€¼ã€æœ€å¤§å€¼å’Œæ­¥é•¿ã€‚çº¬åº¦èŒƒå›´æ˜¯ [-90, 90]ï¼Œæ­¥é•¿ 0.1ï¼›ç»åº¦èŒƒå›´æ˜¯ [-180, 180]ï¼Œæ­¥é•¿ 1.0ï¼›æ—¶åŒºèŒƒå›´æ˜¯ [-12, 14]ï¼Œæ­¥é•¿ 0.5ã€‚ç”¨æˆ·ä¹Ÿå¯ä»¥æ‰‹åŠ¨è¾“å…¥æ›´ç²¾ç¡®çš„å€¼ï¼Œå¹¶ä¸ä¸€å®šè¦éµå¾ªæ­¥é•¿çš„æ•´æ•°å€ã€‚
        - `digits` å±æ€§ç”¨äºæ§åˆ¶æ˜¾ç¤ºçš„å°æ•°ä½æ•°ã€‚
        - å…¶ `value_changed` ä¿¡å·åœ¨æ•°å€¼å‘ç”Ÿæ”¹å˜æ—¶è§¦å‘ï¼Œç¬”è€…è¿æ¥æ­¤ä¿¡å·åˆ°å›è°ƒå‡½æ•°ï¼Œä»¥å®æ—¶æ›´æ–°å›¾è¡¨æ•°æ®å¹¶é‡ç»˜ã€‚
- **æ—¥æœŸé€‰æ‹©**ï¼š
    - **æ—¥å†æ§ä»¶ `Gtk.Calendar`** (`calendar`)ï¼šæä¾›äº†ä¸€ä¸ªæ ‡å‡†çš„æ—¥å†è§†å›¾ï¼Œå…è®¸ç”¨æˆ·æ–¹ä¾¿åœ°é€‰æ‹©æ—¥æœŸã€‚
    - `day_selected` ä¿¡å·åœ¨ç”¨æˆ·ç‚¹å‡»å¹¶é€‰æ‹©ä¸€ä¸ªæ–°çš„æ—¥æœŸæ—¶å‘å‡ºã€‚ç¬”è€…è¿æ¥æ­¤ä¿¡å·åˆ°å›è°ƒå‡½æ•°ï¼Œå½“æ—¥æœŸæ”¹å˜æ—¶ï¼Œæ›´æ–°å›¾è¡¨æ•°æ®å¹¶é‡ç»˜ã€‚`calendar.get_date()` ç”¨äºè·å–å½“å‰é€‰ä¸­çš„ `DateTime` å¯¹è±¡ã€‚
- **å¯¼å‡º**ï¼š
    - â€œExport Imageâ€æŒ‰é’® (`export_button`) å’Œ â€œExport CSVâ€æŒ‰é’® (`export_csv_button`)ï¼šä¸å‰ä¸€ä¸ªä¾‹å­ç±»ä¼¼ï¼Œä½¿ç”¨ `Gtk.Button`ã€‚ç‚¹å‡»åå‡é€šè¿‡ `Gtk.FileDialog` å¼¹å‡ºæ–‡ä»¶ä¿å­˜å¯¹è¯æ¡†ã€‚
        - å›¾åƒå¯¼å‡ºæ”¯æŒ PNG, SVG, PDF æ ¼å¼ï¼Œé€šè¿‡ `Gtk.FileFilter` å®ç°ã€‚
        - CSV å¯¼å‡ºåˆ™åªæä¾› CSV æ ¼å¼çš„è¿‡æ»¤å™¨ã€‚
    - å¯¼å‡ºé€»è¾‘å°è£…åœ¨ `export_chart (file)` å’Œ `export_csv_data (file)` æ–¹æ³•ä¸­ã€‚
- **å›¾è¡¨äº¤äº’**ï¼š
    - **æ‰‹åŠ¿æ§åˆ¶å™¨ `Gtk.GestureClick`**ï¼šä½¿ç”¨æ‰‹åŠ¿ï¼ˆGestureï¼‰ç³»ç»Ÿæ¥å¤„ç†è¾“å…¥äº‹ä»¶ã€‚ç¬”è€…å°†ä¸€ä¸ª `Gtk.GestureClick` æ§åˆ¶å™¨é™„åŠ åˆ° `drawing_area` ä¸Šï¼Œä»¥æ•è·é¼ æ ‡ç‚¹å‡»äº‹ä»¶ã€‚
    - `click_controller.pressed.connect(on_chart_clicked)`ï¼šå½“ç”¨æˆ·åœ¨ `drawing_area` ä¸ŠæŒ‰ä¸‹é¼ æ ‡æŒ‰é’®æ—¶ï¼ˆæ‰‹åŠ¿å¼€å§‹æ—¶ï¼‰ï¼Œ`pressed` ä¿¡å·ä¼šå‘å‡ºï¼Œå¹¶è°ƒç”¨ `on_chart_clicked` å›è°ƒå‡½æ•°ã€‚æ­¤å›è°ƒå‡½æ•°æ¥æ”¶æŒ‰ä¸‹æ¬¡æ•¸ (`n_press`) ä»¥åŠç‚¹å‡»ä½ç½®çš„xã€yåæ ‡ã€‚
    - `on_chart_clicked (int n_press, double x, double y)` å›è°ƒå‡½æ•°ï¼š
        - é¦–å…ˆåˆ¤æ–­ç‚¹å‡»æ˜¯å¦åœ¨ç»˜å›¾åŒºåŸŸå†…ä¸”ä¸ºå•å‡» (`n_press == 1`)ã€‚
        - å¦‚æœæ˜¯æœ‰æ•ˆå•å‡»ï¼Œåˆ™æ ¹æ®ç‚¹å‡»çš„ `x` åæ ‡è®¡ç®—å¯¹åº”çš„æ—¶é—´æµ®ç‚¹æ•°ï¼ˆå°æ—¶ï¼‰ï¼Œå­˜å‚¨åˆ° `clicked_time_hours`ã€‚
        - è®¡ç®—å‡ºå¯¹åº”çš„åˆ†é’Ÿç´¢å¼•ï¼ˆ`time_minutes`ï¼‰ï¼Œå¹¶ä» `sun_angles` æ•°ç»„ä¸­è·å–è¯¥æ—¶é—´ç‚¹çš„å¤ªé˜³é«˜åº¦è§’ï¼Œå­˜å‚¨åˆ° `corresponding_angle`ã€‚
        - è®¾ç½® `has_click_point = true`ã€‚
        - åœ¨ `click_info_label` (ä¸€ä¸ª `Gtk.Label`) ä¸­æ˜¾ç¤ºæ ¼å¼åŒ–åçš„æ—¶é—´å’Œå¤ªé˜³é«˜åº¦è§’ä¿¡æ¯ã€‚
        - è°ƒç”¨ `drawing_area.queue_draw()` è¯·æ±‚é‡ç»˜ï¼Œä»¥ä¾¿åœ¨å›¾è¡¨ä¸Šæ˜¾ç¤ºæ ‡è®°ç‚¹å’Œè¾…åŠ©çº¿ã€‚
        - å¦‚æœæ˜¯åŒå‡»æˆ–åœ¨ç»˜å›¾åŒºåŸŸå¤–ç‚¹å‡»ï¼Œåˆ™æ¸…é™¤é€‰ä¸­çš„æ•°æ®ç‚¹ä¿¡æ¯ï¼ˆè®¾ç½® `has_click_point = false`ï¼Œé‡ç½® `click_info_label` çš„æ–‡æœ¬ï¼‰ï¼Œå¹¶é‡ç»˜å›¾è¡¨ã€‚
- **`activate()` æ–¹æ³•**ï¼š
    - æ­¤æ–¹æ³•è´Ÿè´£åœ¨åº”ç”¨å¯åŠ¨æ—¶æ„å»ºæ•´ä¸ªç”¨æˆ·ç•Œé¢ã€‚
    - åˆå§‹åŒ–æ‰€æœ‰UIç»„ä»¶ï¼ŒåŒ…æ‹¬å„ç§ `Gtk.Box`ã€`Gtk.Grid`ã€`Gtk.Label`ã€`Gtk.SpinButton`ã€`Gtk.Calendar`ã€`Gtk.Button` å’Œ `Gtk.DrawingArea`ã€‚
    - åˆ›å»ºå¹¶é…ç½® `Gtk.GestureClick` æ§åˆ¶å™¨ï¼Œå°†å…¶ `pressed` ä¿¡å·è¿æ¥åˆ° `on_chart_clicked` å›è°ƒå‡½æ•°ï¼Œç„¶åé€šè¿‡ `drawing_area.add_controller(click_controller)` å°†æ‰‹åŠ¿æ§åˆ¶å™¨æ·»åŠ åˆ°ç»˜å›¾åŒºåŸŸï¼Œä½¿å…¶èƒ½å¤Ÿå“åº”ç‚¹å‡»äº‹ä»¶ã€‚
    - æœ€åï¼Œå°†ä¸»å¸ƒå±€ `main_box` è®¾ç½®ä¸ºçª—å£çš„å­æ§ä»¶ï¼Œå¹¶è°ƒç”¨ `window.present()` æ˜¾ç¤ºçª—å£ã€‚

## ç»˜å›¾å‡½æ•°

- `draw_sun_angle_chart (Gtk.DrawingArea, Cairo.Context, int width, int height)`ï¼š
  * ç»˜åˆ¶ç™½è‰²èƒŒæ™¯ã€‚
  * ç»˜åˆ¶åŠé€æ˜ç°è‰²é˜´å½±çŸ©å½¢è¡¨ç¤ºåœ°å¹³çº¿ä»¥ä¸‹åŒºåŸŸã€‚
  * ç»˜åˆ¶ç½‘æ ¼çº¿ï¼šæ°´å¹³çº¿é—´éš” 15Â° é«˜åº¦è§’ï¼Œå‚ç›´çº¿é—´éš” 2 å°æ—¶ã€‚
  * ç»˜åˆ¶åæ ‡è½´ã€åˆ»åº¦æ ‡è®°å’Œæ•°å­—æ ‡ç­¾ã€‚
  * ä½¿ç”¨çº¢è‰²æ›²çº¿ç»˜åˆ¶è®¡ç®—å¾—åˆ°çš„å¤ªé˜³é«˜åº¦è§’éšæ—¶é—´å˜åŒ–ã€‚
  * **å¦‚æœ `has_click_point` ä¸º `true`**ï¼š
     - æ ¹æ®å­˜å‚¨çš„ `clicked_time_hours` å’Œ `corresponding_angle` è®¡ç®—æ ‡è®°ç‚¹çš„ x å’Œ y åæ ‡ã€‚
     - åœ¨æ ‡è®°ç‚¹å¤„ç»˜åˆ¶ä¸€ä¸ªè“è‰²åœ†ç‚¹ã€‚
     - ä»æ ‡è®°ç‚¹å‘å›¾è¡¨çš„é¡¶éƒ¨å’Œåº•éƒ¨ç»˜åˆ¶ä¸€æ¡åŠé€æ˜çš„è“è‰²å‚ç›´çº¿ã€‚
     - ä»æ ‡è®°ç‚¹å‘å›¾è¡¨çš„å·¦ä¾§å’Œå³ä¾§ç»˜åˆ¶ä¸€æ¡åŠé€æ˜çš„è“è‰²æ°´å¹³çº¿ã€‚
  * åœ¨å›¾è¡¨é¡¶éƒ¨ç»˜åˆ¶æ ‡é¢˜ï¼Œåˆ†ä¸¤è¡Œæ˜¾ç¤ºå½“å‰é€‰æ‹©çš„æ—¥æœŸã€çº¬åº¦ã€ç»åº¦å’Œæ—¶åŒºã€‚

## å®ç°ä»£ç 

å®ç°è¿™ä¸ªåº”ç”¨ç¨‹åºçš„ä»£ç å¦‚ä¸‹ï¼š

```vala
#!/usr/bin/env -S vala --pkg=gtk4 -X -lm -X -O2 -X -march=native -X -pipe

/**
 * Solar Angle Calculator Application.
 *
 * A GTK4 application that calculates and visualizes solar elevation angles
 * throughout the day for a given location and date. The application provides
 * an interactive interface for setting latitude, longitude, timezone, and date,
 * and displays a real-time chart of solar elevation angles with export capabilities.
 */
public class SolarAngleApp : Gtk.Application {
    // Constants for solar angle calculations
    private const double DEG2RAD = Math.PI / 180.0;
    private const double RAD2DEG = 180.0 / Math.PI;
    private const int RESOLUTION_PER_MIN = 1440; // 1 sample per minute
    // Constants for margins in the drawing area
    private const int MARGIN_LEFT = 70;
    private const int MARGIN_RIGHT = 20;
    private const int MARGIN_TOP = 50;
    private const int MARGIN_BOTTOM = 70;

    private Gtk.ApplicationWindow window;
    private Gtk.DrawingArea drawing_area;
    private Gtk.Label click_info_label;
    private DateTime selected_date;
    private double sun_angles[RESOLUTION_PER_MIN];
    private double latitude = 0.0;
    private double longitude = 0.0;
    private double timezone_offset_hours = 0.0;
    private double clicked_time_hours = 0.0;
    private double corresponding_angle = 0.0;
    private bool has_click_point = false;

    /**
     * Creates a new SolarAngleApp instance.
     *
     * Initializes the application with a unique application ID and sets
     * the selected date to the current local date.
     */
    public SolarAngleApp () {
        Object (application_id: "com.github.wszqkzqk.SolarAngleGtk");
        selected_date = new DateTime.now_local ();
    }

    /**
     * Activates the application and creates the main window.
     *
     * Sets up the user interface including input controls, drawing area,
     * and initializes the plot data with current settings.
     */
    protected override void activate () {
        window = new Gtk.ApplicationWindow (this) {
            title = "Solar Angle Calculator",
            default_width = 1000,
            default_height = 700,
        };

        var main_box = new Gtk.Box (Gtk.Orientation.HORIZONTAL, 0);

        var left_panel = new Gtk.Box (Gtk.Orientation.VERTICAL, 15) {
            hexpand = false,
            margin_start = 10,
            margin_end = 10,
            margin_top = 10,
            margin_bottom = 10,
        };

        var location_time_group = new Gtk.Box (Gtk.Orientation.VERTICAL, 8);
        var location_time_label = new Gtk.Label ("<b>Location and Time Settings</b>") {
            use_markup = true,
            halign = Gtk.Align.START,
        };
        location_time_group.append (location_time_label);

        var settings_grid = new Gtk.Grid () {
            column_spacing = 10,
            row_spacing = 8,
            margin_top = 5,
        };

        var latitude_label = new Gtk.Label ("Latitude (deg):") {
            halign = Gtk.Align.START,
        };
        var latitude_spin = new Gtk.SpinButton.with_range (-90, 90, 0.1) {
            value = latitude,
            digits = 2,
        };
        latitude_spin.value_changed.connect (() => {
            latitude = latitude_spin.value;
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        var longitude_label = new Gtk.Label ("Longitude (deg):") {
            halign = Gtk.Align.START,
        };
        var longitude_spin = new Gtk.SpinButton.with_range (-180.0, 180.0, 0.1) {
            value = longitude,
            digits = 2,
        };
        longitude_spin.value_changed.connect (() => {
            longitude = longitude_spin.value;
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        var timezone_label = new Gtk.Label ("Timezone (hour):") {
            halign = Gtk.Align.START,
        };
        var timezone_spin = new Gtk.SpinButton.with_range (-12.0, 14.0, 0.5) {
            value = timezone_offset_hours,
            digits = 2,
        };
        timezone_spin.value_changed.connect (() => {
            timezone_offset_hours = timezone_spin.value;
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        settings_grid.attach (latitude_label, 0, 0, 1, 1);
        settings_grid.attach (latitude_spin, 1, 0, 1, 1);
        settings_grid.attach (longitude_label, 0, 1, 1, 1);
        settings_grid.attach (longitude_spin, 1, 1, 1, 1);
        settings_grid.attach (timezone_label, 0, 2, 1, 1);
        settings_grid.attach (timezone_spin, 1, 2, 1, 1);

        location_time_group.append (settings_grid);

        var date_group = new Gtk.Box (Gtk.Orientation.VERTICAL, 8);
        var date_label = new Gtk.Label ("<b>Date Selection</b>") {
            use_markup = true,
            halign = Gtk.Align.START,
        };
        var calendar = new Gtk.Calendar ();
        calendar.day_selected.connect (() => {
            selected_date = calendar.get_date ();
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        date_group.append (date_label);
        date_group.append (calendar);

        var export_group = new Gtk.Box (Gtk.Orientation.VERTICAL, 8);
        var export_label = new Gtk.Label ("<b>Export</b>") {
            use_markup = true,
            halign = Gtk.Align.START,
        };

        // Create horizontal box for buttons
        var export_buttons_box = new Gtk.Box (Gtk.Orientation.HORIZONTAL, 5) {
            homogeneous = true,
        };

        var export_button = new Gtk.Button.with_label ("Export Image");
        export_button.clicked.connect (on_export_clicked);

        var export_csv_button = new Gtk.Button.with_label ("Export CSV");
        export_csv_button.clicked.connect (on_export_csv_clicked);

        export_buttons_box.append (export_button);
        export_buttons_box.append (export_csv_button);

        export_group.append (export_label);
        export_group.append (export_buttons_box);

        // Add click info display group
        var click_info_group = new Gtk.Box (Gtk.Orientation.VERTICAL, 8);
        var click_info_title = new Gtk.Label ("<b>Selected Point</b>") {
            use_markup = true,
            halign = Gtk.Align.START,
        };
        // Initial click info label (Use an extra newline for better spacing)
        click_info_label = new Gtk.Label ("Click on chart to view data\n") {
            halign = Gtk.Align.START,
        };
        click_info_group.append (click_info_title);
        click_info_group.append (click_info_label);

        left_panel.append (location_time_group);
        left_panel.append (date_group);
        left_panel.append (export_group);
        left_panel.append (click_info_group);

        drawing_area = new Gtk.DrawingArea () {
            hexpand = true,
            vexpand = true,
            width_request = 600,
            height_request = 500,
        };
        drawing_area.set_draw_func (draw_sun_angle_chart);

        // Add click event controller
        var click_controller = new Gtk.GestureClick ();
        click_controller.pressed.connect (on_chart_clicked);
        drawing_area.add_controller (click_controller);

        main_box.append (left_panel);
        main_box.append (drawing_area);

        update_plot_data ();

        window.child = main_box;
        window.present ();
    }

    /**
     * Calculates solar elevation angles for each minute of the day.
     *
     * @param latitude_rad Latitude in radians.
     * @param day_of_year Day of the year (1-365/366).
     * @param year The year.
     * @param longitude_deg Longitude in degrees.
     * @param timezone_offset_hrs Timezone offset from UTC in hours.
     */
    private void generate_sun_angles (double latitude_rad, int day_of_year, int year, double longitude_deg, double timezone_offset_hrs) {
        double sin_lat = Math.sin (latitude_rad);
        double cos_lat = Math.cos (latitude_rad);

        double days_in_year = ((year % 4 == 0) && ((year % 100 != 0) || (year % 400 == 0))) ? 366.0 : 365.0;

        for (int i = 0; i < RESOLUTION_PER_MIN; i += 1) {
            // fractional_day_component: day of year plus fraction of the day
            double fractional_day_component = day_of_year - 1 + ((double) i) / RESOLUTION_PER_MIN;
            // gamma: fractional year angle in radians
            double gamma_rad = (2.0 * Math.PI / days_in_year) * fractional_day_component;

            // Solar declination delta (rad) via Fourier series approximation
            double decl_rad = 0.006918
                - 0.399912 * Math.cos (gamma_rad)
                + 0.070257 * Math.sin (gamma_rad)
                - 0.006758 * Math.cos (2.0 * gamma_rad)
                + 0.000907 * Math.sin (2.0 * gamma_rad)
                - 0.002697 * Math.cos (3.0 * gamma_rad)
                + 0.001480 * Math.sin (3.0 * gamma_rad);

            // Equation of Time (EoT) in minutes
            double eqtime_minutes = 229.18 * (0.000075
                + 0.001868 * Math.cos (gamma_rad)
                - 0.032077 * Math.sin (gamma_rad)
                - 0.014615 * Math.cos (2.0 * gamma_rad)
                - 0.040849 * Math.sin (2.0 * gamma_rad));

            // True Solar Time (TST) in minutes, correcting local clock by EoT and longitude
            double tst_minutes = i + eqtime_minutes + 4.0 * longitude_deg - 60.0 * timezone_offset_hrs;

            // Hour angle H (Â°) relative to solar noon
            double ha_deg = tst_minutes / 4.0 - 180.0;
            double ha_rad = ha_deg * DEG2RAD;

            // cos(phi): cosine of zenith angle via spherical trig
            double cos_phi = sin_lat * Math.sin (decl_rad) + cos_lat * Math.cos (decl_rad) * Math.cos (ha_rad);
            // clamp to valid range
            if (cos_phi > 1.0) cos_phi = 1.0;
            if (cos_phi < -1.0) cos_phi = -1.0;
            // Zenith angle phi (rad)
            double phi_rad = Math.acos (cos_phi);

            // Solar elevation alpha = 90Â° - phi, convert to degrees
            double solar_elevation_rad = Math.PI / 2.0 - phi_rad;
            sun_angles[i] = solar_elevation_rad * RAD2DEG;
        }
    }

    /**
     * Updates solar angle data for current settings.
     */
    private void update_plot_data () {
        int day_of_year = selected_date.get_day_of_year ();
        double latitude_rad = latitude * DEG2RAD;
        int year = selected_date.get_year ();
        generate_sun_angles (latitude_rad, day_of_year, year, longitude, timezone_offset_hours);
        
        // Clear click point when data updates
        has_click_point = false;
        click_info_label.label = "Click on chart to view data\n";
    }

    /**
     * Handles mouse click events on the chart.
     *
     * @param n_press Number of button presses.
     * @param x X coordinate of the click.
     * @param y Y coordinate of the click.
     */
    private void on_chart_clicked (int n_press, double x, double y) {
        int width = drawing_area.get_width ();
        int height = drawing_area.get_height ();

        int chart_width = width - MARGIN_LEFT - MARGIN_RIGHT;

        // Check if click is within plot area and single click
        if (x >= MARGIN_LEFT && x <= width - MARGIN_RIGHT && y >= MARGIN_TOP && y <= height - MARGIN_BOTTOM && n_press == 1) {
            // Convert coordinates to time and get corresponding angle
            clicked_time_hours = (x - MARGIN_LEFT) / chart_width * 24.0;
            int time_minutes = (int) (clicked_time_hours * 60) % RESOLUTION_PER_MIN;
            corresponding_angle = sun_angles[time_minutes];
            has_click_point = true;

            // Format time display
            int hours = (int) clicked_time_hours;
            int minutes = (int) ((clicked_time_hours - hours) * 60);

            // Update info label
            string info_text = "Time: %02d:%02d\nSolar Elevation: %.1fÂ°".printf (
                hours, minutes, corresponding_angle
            );

            click_info_label.label = info_text;
            drawing_area.queue_draw ();
        } else {
            // Double click or outside plot area - clear point
            has_click_point = false;
            click_info_label.label = "Click on chart to view data\n";
            drawing_area.queue_draw ();
        }
    }

    /**
     * Draws the solar elevation chart.
     *
     * @param area The drawing area widget.
     * @param cr The Cairo context for drawing.
     * @param width The width of the drawing area.
     * @param height The height of the drawing area.
     */
    private void draw_sun_angle_chart (Gtk.DrawingArea area, Cairo.Context cr, int width, int height) {
        // Fill background
        cr.set_source_rgb (1, 1, 1);
        cr.paint ();

        int chart_width = width - MARGIN_LEFT - MARGIN_RIGHT;
        int chart_height = height - MARGIN_TOP - MARGIN_BOTTOM;

        double horizon_y = MARGIN_TOP + chart_height * 0.5; // 0Â° is at middle of -90Â° to +90Â° range
        
        // Shade area below horizon
        cr.set_source_rgba (0.7, 0.7, 0.7, 0.3);
        cr.rectangle (MARGIN_LEFT, horizon_y, chart_width, height - MARGIN_BOTTOM - horizon_y);
        cr.fill ();

        // Draw horizontal grid every 15Â°
        cr.set_source_rgba (0.5, 0.5, 0.5, 0.5);
        cr.set_line_width (1);
        for (int angle = -90; angle <= 90; angle += 15) {
            double tick_y = MARGIN_TOP + chart_height * (90 - angle) / 180.0;
            cr.move_to (MARGIN_LEFT, tick_y);
            cr.line_to (width - MARGIN_RIGHT, tick_y);
            cr.stroke ();
        }
        // Draw vertical grid every 2 hours
        for (int h = 0; h <= 24; h += 2) {
            double tick_x = MARGIN_LEFT + chart_width * (h / 24.0);
            cr.move_to (tick_x, MARGIN_TOP);
            cr.line_to (tick_x, height - MARGIN_BOTTOM);
            cr.stroke ();
        }

        // Draw axes and horizon
        cr.set_source_rgb (0, 0, 0);
        cr.set_line_width (2);
        cr.move_to (MARGIN_LEFT, height - MARGIN_BOTTOM);
        cr.line_to (width - MARGIN_RIGHT, height - MARGIN_BOTTOM);
        cr.stroke ();
        cr.move_to (MARGIN_LEFT, MARGIN_TOP);
        cr.line_to (MARGIN_LEFT, height - MARGIN_BOTTOM);
        cr.stroke ();
        // Horizon line
        cr.move_to (MARGIN_LEFT, horizon_y);
        cr.line_to (width - MARGIN_RIGHT, horizon_y);
        cr.stroke ();

        // Draw axis ticks and labels
        cr.set_line_width (1);
        cr.set_font_size (20);
        for (int angle = -90; angle <= 90; angle += 15) {
            double tick_y = MARGIN_TOP + chart_height * (90 - angle) / 180.0;
            cr.move_to (MARGIN_LEFT - 5, tick_y);
            cr.line_to (MARGIN_LEFT, tick_y);
            cr.stroke ();
            var te = Cairo.TextExtents ();
            var txt = angle.to_string ();
            cr.text_extents (txt, out te);
            cr.move_to (MARGIN_LEFT - 10 - te.width, tick_y + te.height / 2);
            cr.show_text (txt);
        }
        for (int h = 0; h <= 24; h += 2) {
            double tick_x = MARGIN_LEFT + chart_width * (h / 24.0);
            cr.move_to (tick_x, height - MARGIN_BOTTOM);
            cr.line_to (tick_x, height - MARGIN_BOTTOM + 5);
            cr.stroke ();
            var te = Cairo.TextExtents ();
            var txt = h.to_string ();
            cr.text_extents (txt, out te);
            cr.move_to (tick_x - te.width / 2, height - MARGIN_BOTTOM + 25);
            cr.show_text (txt);
        }

        // Plot solar elevation curve
        cr.set_source_rgb (1, 0, 0);
        cr.set_line_width (2);
        for (int i = 0; i < RESOLUTION_PER_MIN; i += 1) {
            double x = MARGIN_LEFT + chart_width * (i / (double) (RESOLUTION_PER_MIN - 1));
            double y = MARGIN_TOP + chart_height * (90.0 - sun_angles[i]) / 180.0;
            if (i == 0) {
                cr.move_to (x, y);
            } else {
                cr.line_to (x, y);
            }
        }
        cr.stroke ();

        // Draw click point if exists
        if (has_click_point) {
            // Calculate current coordinates from stored time and angle
            double clicked_x = MARGIN_LEFT + chart_width * (clicked_time_hours / 24.0);
            double corresponding_y = MARGIN_TOP + chart_height * (90.0 - corresponding_angle) / 180.0;

            cr.set_source_rgba (0, 0, 1, 0.8);
            cr.arc (clicked_x, corresponding_y, 5, 0, 2 * Math.PI);
            cr.fill ();
            
            // Draw vertical line to show time
            cr.set_source_rgba (0, 0, 1, 0.5);
            cr.set_line_width (1);
            cr.move_to (clicked_x, MARGIN_TOP);
            cr.line_to (clicked_x, height - MARGIN_BOTTOM);
            cr.stroke ();
            
            // Draw horizontal line to show angle
            cr.move_to (MARGIN_LEFT, corresponding_y);
            cr.line_to (width - MARGIN_RIGHT, corresponding_y);
            cr.stroke ();
        }

        // Draw axis titles
        cr.set_source_rgb (0, 0, 0);
        cr.set_font_size (20);
        string x_title = "Time (Hour)";
        Cairo.TextExtents x_ext;
        cr.text_extents (x_title, out x_ext);
        cr.move_to ((double) width / 2 - x_ext.width / 2, height - MARGIN_BOTTOM + 55);
        cr.show_text (x_title);
        string y_title = "Solar Elevation (Â°)";
        Cairo.TextExtents y_ext;
        cr.text_extents (y_title, out y_ext);
        cr.save ();
        cr.translate (MARGIN_LEFT - 45, (double)height / 2);
        cr.rotate (-Math.PI / 2);
        cr.move_to (-y_ext.width / 2, 0);
        cr.show_text (y_title);
        cr.restore ();

        // Draw chart captions
        string caption_line1 = "Solar Elevation Angle - Date: %s".printf (selected_date.format ("%Y-%m-%d"));
        string caption_line2 = "Lat: %.2fÂ°, Lon: %.2fÂ°, TZ: UTC%+.2f".printf (latitude, longitude, timezone_offset_hours);
        
        cr.set_font_size (18);
        Cairo.TextExtents cap_ext1, cap_ext2;
        cr.text_extents (caption_line1, out cap_ext1);
        cr.text_extents (caption_line2, out cap_ext2);

        double total_caption_height = cap_ext1.height + cap_ext2.height + 5;

        cr.move_to ((width - cap_ext1.width) / 2, (MARGIN_TOP - total_caption_height) / 2 + cap_ext1.height);
        cr.show_text (caption_line1);
        cr.move_to ((width - cap_ext2.width) / 2, (MARGIN_TOP - total_caption_height) / 2 + cap_ext1.height + 5 + cap_ext2.height);
        cr.show_text (caption_line2);
    }

    /**
     * Handles export button click event.
     *
     * Shows a file save dialog with filters for PNG, SVG, and PDF formats.
     */
    private void on_export_clicked () {
        // Show save dialog with PNG, SVG, PDF filters
        var png_filter = new Gtk.FileFilter ();
        png_filter.name = "PNG Images";
        png_filter.add_mime_type ("image/png");
        
        var svg_filter = new Gtk.FileFilter ();
        svg_filter.name = "SVG Images";
        svg_filter.add_mime_type ("image/svg+xml");

        var pdf_filter = new Gtk.FileFilter ();
        pdf_filter.name = "PDF Documents";
        pdf_filter.add_mime_type ("application/pdf");

        var filter_list = new ListStore (typeof (Gtk.FileFilter));
        filter_list.append (png_filter);
        filter_list.append (svg_filter);
        filter_list.append (pdf_filter);

        var file_dialog = new Gtk.FileDialog () {
            modal = true,
            initial_name = "solar_elevation_chart.png",
            filters = filter_list,
        };

        file_dialog.save.begin (window, null, (obj, res) => {
            try {
                var file = file_dialog.save.end (res);
                if (file != null) {
                    export_chart (file);
                }
            } catch (Error e) {
                message ("Image file has not been saved: %s", e.message);
            }
        });
    }

    /**
     * Exports the current chart to a file.
     *
     * Supports PNG, SVG, and PDF formats based on file extension.
     * Defaults to PNG if extension is not recognized.
     *
     * @param file The file to export the chart to.
     */
    private void export_chart (File file) {
        int width = drawing_area.get_width ();
        int height = drawing_area.get_height ();

        if (width <= 0 || height <= 0) {
            width = 800;
            height = 600;
        }

        string filepath = file.get_path ();
        string? extension = null;
        var last_dot = filepath.last_index_of_char ('.');
        if (last_dot != -1) {
            extension = filepath[last_dot:].down ();
        }

        if (extension == ".svg") {
            Cairo.SvgSurface surface = new Cairo.SvgSurface (filepath, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_sun_angle_chart (drawing_area, cr, width, height);
        } else if (extension == ".pdf") {
            Cairo.PdfSurface surface = new Cairo.PdfSurface (filepath, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_sun_angle_chart (drawing_area, cr, width, height);
        } else {
            Cairo.ImageSurface surface = new Cairo.ImageSurface (Cairo.Format.RGB24, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_sun_angle_chart (drawing_area, cr, width, height);
            surface.write_to_png (filepath);
        }
    }

    /**
     * Handles CSV export button click event.
     *
     * Shows a file save dialog for CSV format.
     */
    private void on_export_csv_clicked () {
        var csv_filter = new Gtk.FileFilter ();
        csv_filter.name = "CSV Files";
        csv_filter.add_mime_type ("text/csv");

        var filter_list = new ListStore (typeof (Gtk.FileFilter));
        filter_list.append (csv_filter);

        var file_dialog = new Gtk.FileDialog () {
            modal = true,
            initial_name = "solar_elevation_data.csv",
            filters = filter_list,
        };

        file_dialog.save.begin (window, null, (obj, res) => {
            try {
                var file = file_dialog.save.end (res);
                if (file != null) {
                    export_csv_data (file);
                }
            } catch (Error e) {
                message ("CSV file has not been saved: %s", e.message);
            }
        });
    }

    /**
     * Exports the solar elevation data to a CSV file.
     *
     * @param file The file to export the data to.
     */
    private void export_csv_data (File file) {
        try {
            var stream = file.replace (null, false, FileCreateFlags.REPLACE_DESTINATION);
            var data_stream = new DataOutputStream (stream);

            // Write CSV metadata as comments
            data_stream.put_string ("# Solar Elevation Data\n");
            data_stream.put_string ("# Date: %s\n".printf (selected_date.format ("%Y-%m-%d")));
            data_stream.put_string ("# Latitude: %.2f degrees\n".printf (latitude));
            data_stream.put_string ("# Longitude: %.2f degrees\n".printf (longitude));
            data_stream.put_string ("# Timezone: UTC%+.2f\n".printf (timezone_offset_hours));
            data_stream.put_string ("#\n");

            // Write CSV header
            data_stream.put_string ("Time,Solar Elevation (degrees)\n");

            // Write data points
            for (int i = 0; i < RESOLUTION_PER_MIN; i += 1) {
                int hours = i / 60;
                int minutes = i % 60;
                data_stream.put_string (
                    "%02d:%02d,%.3f\n".printf (hours, minutes, sun_angles[i])
                );
            }

            data_stream.close ();
        } catch (Error e) {
            message ("Error saving CSV file: %s", e.message);
        }
    }

    /**
     * Application entry point.
     *
     * Creates and runs the SolarAngleApp instance.
     *
     * @param args Command line arguments.
     * @return Exit code.
     */
    public static int main (string[] args) {
        var app = new SolarAngleApp ();
        return app.run (args);
    }
}
```

## ç¼–è¯‘ä¸è¿è¡Œè¯´æ˜
- æ–‡ä»¶æœ€ä¸Šæ–¹çš„ shebang æŒ‡å®šäº†ç¼–è¯‘å‘½ä»¤ï¼Œå…¶åŒ…å«äº†ï¼š
  - æŒ‡å®š Vala ç¼–è¯‘å™¨ï¼ˆ`#!/usr/bin/env -S vala`ï¼‰ã€‚
  - æ·»åŠ  gtk4 åŒ…ï¼ˆ`--pkg=gtk4`ï¼‰ã€‚
  - é“¾æ¥æ•°å­¦åº“ (`-lm`)ã€‚
  - å„ç§ç¼–è¯‘ä¼˜åŒ–å‚æ•°ï¼ˆå¦‚ `-O2`ã€`-march=native` ç­‰ï¼‰ã€‚
- è¿è¡Œå‰è¯·ç¡®ä¿ç³»ç»Ÿå·²å®‰è£… Valaã€GTK4 ä»¥åŠç›¸å…³ç¼–è¯‘å·¥å…·ï¼ˆå¦‚ `gcc`ï¼‰ã€‚
  - åœ¨ Arch Linux ä¸Šï¼Œå¯ä»¥ä½¿ç”¨ä»¥ä¸‹å‘½ä»¤å®‰è£…æ‰€éœ€çš„åŒ…ï¼š
    ```bash
    sudo pacman -S --needed vala gtk4
    ```
  - Windows ç”¨æˆ·å¯ä»¥ä½¿ç”¨ MSYS2 ç¯å¢ƒï¼Œå®‰è£…ç›¸å…³åŒ…ï¼š
    ```bash
    pacman -S --needed mingw-w64-ucrt-x86_64-vala mingw-w64-ucrt-x86_64-gcc mingw-w64-ucrt-x86_64-gtk4
    ```
- å¯ä»¥ç›´æ¥æ‰§è¡Œè¯¥è„šæœ¬ï¼Œä¹Ÿå¯ä»¥åœ¨å‘½ä»¤è¡Œä¸­ä½¿ç”¨å¦‚ä¸‹å‘½ä»¤è¿›è¡Œç¼–è¯‘ï¼š
  ```bash
  valac --pkg gtk4 -X -lm -X -pipe -X -O2 solaranglegtk.vala
  ```
  ç¼–è¯‘åå°†ç”Ÿæˆ `solaranglegtk` å¯æ‰§è¡Œæ–‡ä»¶ï¼Œç›´æ¥è¿è¡Œå³å¯ã€‚

# Libadwaita/GTK4åº”ç”¨ï¼šå¤ªé˜³é«˜åº¦è§’è®¡ç®—ä¸ç»˜åˆ¶å·¥å…·

åœ¨å‰é¢çš„ GTK4 ç¤ºä¾‹ä¹‹åï¼Œæˆ‘ä»¬æ¥çœ‹ä¸€ä¸‹å¦‚ä½•ä½¿ç”¨ Libadwaita æ¥æ„å»ºå…·æœ‰ç°ä»£ GNOME æ¡Œé¢é£æ ¼çš„åº”ç”¨ç¨‹åºã€‚Libadwaita æ˜¯ GTK4 çš„å®˜æ–¹é…å¥—åº“ï¼Œæ—¨åœ¨å¸®åŠ©å¼€å‘è€…æ„å»ºéµå¾ª GNOME äººæœºäº¤äº’æŒ‡å— (HIG) çš„åº”ç”¨ç¨‹åºã€‚å®ƒæä¾›äº†ä¸€ç³»åˆ—ç¬¦åˆç°ä»£è®¾è®¡è¶‹åŠ¿çš„ã€è‡ªé€‚åº”çš„æ§ä»¶å’Œå¸ƒå±€å®¹å™¨ï¼Œèƒ½å¤Ÿè®©åº”ç”¨åœ¨ GNOME æ¡Œé¢ç¯å¢ƒä¸­æ‹¥æœ‰ä¸€è‡´ä¸”ç¾è§‚çš„ç”¨æˆ·ä½“éªŒã€‚

å¯¹äº GTK/Vala å¼€å‘è€…è€Œè¨€ï¼Œå¼•å…¥ Libadwaita å¯ä»¥ï¼š
*   **éµå¾ª HIG**ï¼šæ›´å®¹æ˜“åˆ›å»ºç¬¦åˆ GNOME è®¾è®¡è§„èŒƒçš„åº”ç”¨ã€‚
*   **ç°ä»£æ§ä»¶**ï¼šå¯ä»¥ä½¿ç”¨å¦‚ `Adw.HeaderBar`, `Adw.PreferencesGroup`, `Adw.ActionRow`, `Adw.SpinRow` ç­‰ä¸“ä¸ºç°ä»£åº”ç”¨è®¾è®¡çš„æ§ä»¶ã€‚
*   **è‡ªé€‚åº”å¸ƒå±€**ï¼šLibadwaita çš„è®¸å¤šæ§ä»¶å’Œå®¹å™¨éƒ½è€ƒè™‘äº†ä¸åŒçª—å£å¤§å°å’Œè®¾å¤‡çš„é€‚åº”æ€§ã€‚
*   **æ·±è‰²æ¨¡å¼æ”¯æŒ**ï¼šå†…ç½®äº†å¯¹ç³»ç»Ÿæ·±è‰²æ¨¡å¼çš„è‰¯å¥½æ”¯æŒå’Œåˆ‡æ¢èƒ½åŠ›ã€‚

è¿™ä¸ª Libadwaita ç‰ˆæœ¬çš„å¤ªé˜³é«˜åº¦è§’è®¡ç®—å·¥å…·ï¼Œå…¶æ ¸å¿ƒè®¡ç®—é€»è¾‘ä¸ä¸Šä¸€ä¸ª GTK4 ç‰ˆæœ¬å®Œå…¨ç›¸åŒï¼Œä¸»è¦åŒºåˆ«åœ¨äºç”¨æˆ·ç•Œé¢çš„æ„å»ºæ–¹å¼å’Œæ•´ä½“å¤–è§‚é£æ ¼ã€‚æˆ‘ä»¬å°†é‡ç‚¹ä»‹ç» Libadwaita ç‰¹æœ‰çš„æ§ä»¶å’Œæ¦‚å¿µã€‚

## è¿è¡Œæ•ˆæœ

|[![#~/img/GTK-examples/chongqing-solar-angle-adw.webp](/img/GTK-examples/chongqing-solar-angle-spring-adw.webp)](/img/GTK-examples/chongqing-solar-angle-spring-adw.webp)|[![#~/img/GTK-examples/beijing-solar-angle-summer-adw.webp](/img/GTK-examples/beijing-solar-angle-summer-adw.webp)](/img/GTK-examples/beijing-solar-angle-summer-adw.webp)|
|:----:|:----:|
|é‡åº†(åŒ—åŠçƒæ˜¥åˆ†)|åŒ—äº¬(åŒ—åŠçƒå¤è‡³ï¼Œæ·±è‰²æ¨¡å¼)|
|[![#~/img/GTK-examples/singapore-solar-angle-autumn-adw.webp](/img/GTK-examples/singapore-solar-angle-autumn-adw.webp)](/img/GTK-examples/singapore-solar-angle-autumn-adw.webp)|[![#~/img/GTK-examples/n-polar-solar-angle-winter-adw.webp](/img/GTK-examples/n-polar-solar-angle-winter-adw.webp)](/img/GTK-examples/n-polar-solar-angle-winter-adw.webp)|
|æ–°åŠ å¡(åŒ—åŠçƒç§‹åˆ†)|åŒ—æç‚¹(åŒ—åŠçƒå†¬è‡³ï¼Œæ·±è‰²æ¨¡å¼)|

## ç¨‹åºæ¶æ„ (Libadwaita UI ç‰¹ç‚¹)

### åº”ç”¨ä¸çª—å£åŸºç±»
- **`Adw.Application`**ï¼šå–ä»£äº† `Gtk.Application`ï¼Œä½œä¸º Libadwaita åº”ç”¨çš„å…¥å£å’Œç®¡ç†è€…ã€‚
- **`Adw.ApplicationWindow`**ï¼šå–ä»£äº† `Gtk.ApplicationWindow`ï¼Œæ˜¯ Libadwaita åº”ç”¨çš„ä¸»çª—å£ï¼Œèƒ½æ›´å¥½åœ°ä¸ `Adw.HeaderBar` ç­‰ Libadwaita ç»„ä»¶é›†æˆã€‚

### ä¸»ä½“å¸ƒå±€ä¸é¡¶æ 
- **`Adw.ToolbarView`**ï¼šè¿™æ˜¯ä¸€ä¸ªå¼ºå¤§çš„å®¹å™¨ï¼Œç”¨äºç»„ç»‡å¸¦æœ‰å·¥å…·æ ï¼ˆå¦‚ `Adw.HeaderBar`ï¼‰çš„å†…å®¹è§†å›¾ã€‚åœ¨æœ¬ä¾‹ä¸­ï¼Œå®ƒä½œä¸ºçª—å£çš„ä¸»è¦å†…å®¹ï¼Œé¡¶éƒ¨æ·»åŠ äº† `Adw.HeaderBar`ã€‚
- **`Adw.HeaderBar`**ï¼šå–ä»£äº†ä¼ ç»Ÿçš„æ ‡é¢˜æ å’Œ `Gtk.HeaderBar`ï¼Œæä¾›äº†æ›´çµæ´»çš„è‡ªå®šä¹‰ç©ºé—´ã€‚
    - **`Adw.WindowTitle`**ï¼šæ”¾ç½®åœ¨ `Adw.HeaderBar` çš„ `title_widget` å±æ€§ä¸­ï¼Œç”¨äºæ˜¾ç¤ºå±…ä¸­çš„çª—å£æ ‡é¢˜å’Œå‰¯æ ‡é¢˜ï¼ˆæœ¬ä¾‹ä¸­å‰¯æ ‡é¢˜ä¸ºç©ºï¼‰ã€‚
    - **æ·±è‰²æ¨¡å¼åˆ‡æ¢æŒ‰é’® (`Gtk.ToggleButton`)**ï¼šè¢«æ·»åŠ åˆ° `Adw.HeaderBar` çš„æœ«å°¾ (`pack_end`)ã€‚
        - é€šè¿‡ `style_manager.color_scheme` æ¥æ§åˆ¶åº”ç”¨çš„ä¸»é¢˜ï¼ˆå¼ºåˆ¶æµ…è‰²ã€å¼ºåˆ¶æ·±è‰²æˆ–è·Ÿéšç³»ç»Ÿï¼‰ã€‚
        - æŒ‰é’®çš„ `active` çŠ¶æ€ä¸ `style_manager.dark`ï¼ˆä¸€ä¸ªå¸ƒå°”å€¼ï¼ŒæŒ‡ç¤ºå½“å‰æ˜¯å¦ä¸ºæ·±è‰²æ¨¡å¼ï¼‰ç»‘å®šã€‚
        - ç›‘å¬ `style_manager.notify["dark"]` ä¿¡å·ï¼Œä»¥ä¾¿åœ¨ç³»ç»Ÿä¸»é¢˜åˆ‡æ¢æ—¶ï¼ˆå¦‚æœåº”ç”¨è®¾ç½®ä¸ºè·Ÿéšç³»ç»Ÿä¸»é¢˜ï¼‰è‡ªåŠ¨é‡ç»˜å›¾è¡¨ã€‚

### å·¦ä¾§æ§åˆ¶é¢æ¿
å·¦ä¾§é¢æ¿ (`left_panel`) ä¾ç„¶æ˜¯ä¸€ä¸ªå‚ç›´çš„ `Gtk.Box`ï¼Œä½†å…¶å†…éƒ¨çš„è®¾ç½®é¡¹ç»„ç»‡æ–¹å¼å‘ç”Ÿäº†æ˜¾è‘—å˜åŒ–ï¼Œå¤§é‡ä½¿ç”¨äº† Libadwaita çš„åå¥½è®¾ç½®ç›¸å…³æ§ä»¶ï¼Œä½¿å…¶å¤–è§‚æ›´ç¬¦åˆ GNOME è®¾ç½®åº”ç”¨çš„é£æ ¼ã€‚

- **`Adw.PreferencesGroup`**ï¼šç”¨äºå°†ç›¸å…³çš„è®¾ç½®é¡¹ç»„ç»‡åœ¨ä¸€èµ·ï¼Œå¹¶æ˜¾ç¤ºä¸€ä¸ªå¯é€‰çš„æ ‡é¢˜ã€‚æœ¬ä¾‹ä¸­åˆ†åˆ«ç”¨äºâ€œLocation and Time Settingsâ€ã€â€œDate Selectionâ€ã€â€œExportâ€å’Œâ€œSelected Pointâ€è¿™å‡ ç»„ã€‚è¿™æ¯”å•çº¯ä½¿ç”¨ `Gtk.Box` å’Œ `Gtk.Label` ä½œä¸ºæ ‡é¢˜æä¾›äº†æ›´è§„èŒƒå’Œç¾è§‚çš„åˆ†ç»„æ–¹å¼ã€‚
- **ä½ç½®å’Œæ—¶é—´è®¾ç½® (`Adw.PreferencesGroup`)**
    - **`Adw.SpinRow`**ï¼šè¿™æ˜¯ Libadwaita ä¸­ç”¨äºæ•°å­—è¾“å…¥çš„è¡Œæ§ä»¶ï¼Œå®ƒæ•´åˆäº†æ ‡é¢˜ã€å‰¯æ ‡é¢˜ï¼ˆå¯é€‰ï¼‰å’Œä¸€ä¸ª `Gtk.SpinButton`ã€‚ç›¸æ¯”äºä¹‹å‰ GTK4 ç‰ˆæœ¬ä¸­æ‰‹åŠ¨ç»„åˆ `Gtk.Label` å’Œ `Gtk.SpinButton`ï¼Œ`Adw.SpinRow` æä¾›äº†æ›´ç®€æ´ã€ç»Ÿä¸€çš„ HIG é£æ ¼ã€‚
        - `title` å±æ€§è®¾ç½®è¡Œæ ‡é¢˜ï¼ˆå¦‚ "Latitude"ï¼‰ã€‚
        - `subtitle` å±æ€§æä¾›é¢å¤–è¯´æ˜ï¼ˆå¦‚ "Degrees"ï¼‰ã€‚
        - `with_range()` æ„é€ å‡½æ•°è®¾ç½®èŒƒå›´å’Œæ­¥é•¿ã€‚
        - `digits` æ§åˆ¶å°æ•°ä½æ•°ã€‚
        - é€šè¿‡ç›‘å¬å…¶ `notify["value"]` ä¿¡å·æ¥å“åº”å€¼çš„å˜åŒ–ï¼ˆæ³¨æ„ï¼Œè¿™é‡Œä¸æ˜¯ç›´æ¥è¿æ¥ `Gtk.SpinButton` çš„ `value_changed` ä¿¡å·ï¼Œè€Œæ˜¯ç›‘å¬ `Adw.SpinRow` è‡ªèº«çš„ `value` å±æ€§å˜åŒ–é€šçŸ¥ï¼‰ã€‚
- **æ—¥æœŸé€‰æ‹© (`Adw.PreferencesGroup`)**
    - **`Gtk.Calendar`**ï¼šæ—¥å†æ§ä»¶æœ¬èº«ä¿æŒä¸å˜ã€‚
    - **`Adw.ActionRow`**ï¼š`Gtk.Calendar` è¢«æ”¾ç½®åœ¨ä¸€ä¸ª `Adw.ActionRow` ä¸­ï¼Œå†æ·»åŠ åˆ° `Adw.PreferencesGroup`ã€‚`Adw.ActionRow` æ˜¯ä¸€ä¸ªé€šç”¨çš„è¡Œæ§ä»¶ï¼Œå¯ä»¥åŒ…å«è‡ªå®šä¹‰å­æ§ä»¶ï¼Œå¹¶å¯ä»¥è®¾ç½®æ ‡é¢˜ã€å‰¯æ ‡é¢˜ä»¥åŠå¯æ¿€æ´»çŠ¶æ€ã€‚è¿™é‡Œå®ƒä¸»è¦ä½œä¸ºæ—¥å†æ§ä»¶çš„å®¹å™¨ï¼Œä½¿å…¶èå…¥ `PreferencesGroup` çš„æ•´ä½“é£æ ¼ã€‚
- **å¯¼å‡º (`Adw.PreferencesGroup`)**
    - **`Adw.ActionRow`**ï¼šç”¨äºâ€œExport Imageâ€å’Œâ€œExport CSVâ€ä¸¤ä¸ªæ“ä½œã€‚
        - `title` å’Œ `subtitle` æä¾›äº†æ¸…æ™°çš„æ“ä½œè¯´æ˜ã€‚
        - `activatable = true` ä½¿å¾—æ•´è¡Œéƒ½å¯ä»¥è¢«ç‚¹å‡»ä»¥è§¦å‘æ“ä½œã€‚
        - **`add_suffix(Gtk.Widget)`**ï¼šé€šè¿‡æ­¤æ–¹æ³•åœ¨è¡Œçš„æœ«å°¾æ·»åŠ äº†ä¸€ä¸ªå›¾æ ‡æŒ‰é’® (`Gtk.Button`)ï¼Œæä¾›äº†æ˜ç¡®çš„è§†è§‰è§¦å‘ç‚¹ã€‚æŒ‰é’®è®¾ç½®äº† `icon_name` å’Œ `css_classes = { "flat" }` ä»¥è·å¾—æ‰å¹³åŒ–å¤–è§‚ã€‚
        - è¡Œçš„ `activated` ä¿¡å·å’ŒæŒ‰é’®çš„ `clicked` ä¿¡å·éƒ½è¿æ¥åˆ°ç›¸åº”çš„å¯¼å‡ºå¤„ç†å‡½æ•°ã€‚
- **ç‚¹å‡»ä¿¡æ¯ (`Adw.PreferencesGroup`)**
    - **`Gtk.Label`** (`click_info_label`)ï¼šç”¨äºæ˜¾ç¤ºå›¾è¡¨ä¸Šç‚¹å‡»ç‚¹çš„ä¿¡æ¯ï¼ŒåŒæ ·è¢«åŒ…è£…åœ¨ä¸€ä¸ª `Adw.ActionRow` ä¸­ã€‚

### ç»˜å›¾åŒºåŸŸä¸ä¸»é¢˜é€‚é…
- **`Gtk.DrawingArea`** (`drawing_area`)ï¼šç»˜å›¾åŒºåŸŸçš„æ ¸å¿ƒåŠŸèƒ½ä¸å˜ï¼Œä¾ç„¶ä½¿ç”¨ Cairo è¿›è¡Œè‡ªå®šä¹‰ç»˜å›¾ã€‚
- **ä¸»é¢˜é¢œè‰²ç»“æ„ä½“ (`ThemeColors`)**ï¼š
    - ä¸ºäº†è®©ç»˜å›¾åŒºåŸŸçš„é¢œè‰²èƒ½å¤Ÿé€‚åº” Libadwaita çš„æµ…è‰²å’Œæ·±è‰²ä¸»é¢˜ï¼Œå®šä¹‰äº†ä¸€ä¸ª `ThemeColors` ç»“æ„ä½“ï¼Œå…¶ä¸­åŒ…å«äº†èƒŒæ™¯ã€ç½‘æ ¼ã€åæ ‡è½´ã€æ–‡æœ¬ã€æ›²çº¿ã€é˜´å½±åŒºåŸŸã€ç‚¹å‡»ç‚¹æ ‡è®°å’Œè¾…åŠ©çº¿ç­‰å…ƒç´ çš„é¢œè‰²å€¼ã€‚
    - åˆ›å»ºäº† `LIGHT_THEME` å’Œ `DARK_THEME` ä¸¤ä¸ª `ThemeColors` å®ä¾‹ï¼Œåˆ†åˆ«å­˜å‚¨æµ…è‰²å’Œæ·±è‰²æ¨¡å¼ä¸‹çš„é¢œè‰²é…ç½®ã€‚
- **`draw_sun_angle_chart` å‡½æ•°çš„é€‚é…**ï¼š
    - åœ¨ç»˜å›¾å‡½æ•°çš„å¼€å§‹ï¼Œé€šè¿‡ `style_manager.dark` åˆ¤æ–­å½“å‰æ˜¯å¦ä¸ºæ·±è‰²æ¨¡å¼ã€‚
    - æ ¹æ®åˆ¤æ–­ç»“æœé€‰æ‹© `DARK_THEME` æˆ– `LIGHT_THEME` ä¸­çš„é¢œè‰²æ¥è®¾ç½® Cairo çš„ç»˜å›¾æºã€‚è¿™ç¡®ä¿äº†å›¾è¡¨å†…å®¹ï¼ˆå¦‚èƒŒæ™¯ã€æ–‡å­—ã€çº¿æ¡é¢œè‰²ï¼‰ä¸åº”ç”¨æ•´ä½“ä¸»é¢˜ä¿æŒä¸€è‡´ã€‚

### äº‹ä»¶å¤„ç†
- å¤§éƒ¨åˆ†äº‹ä»¶å¤„ç†é€»è¾‘ä¸ GTK4 ç‰ˆæœ¬ç›¸ä¼¼ã€‚
- `Adw.SpinRow` çš„å€¼æ”¹å˜é€šè¿‡ `notify["value"]` ä¿¡å·æ•è·ã€‚
- `Adw.ActionRow` çš„æ¿€æ´»é€šè¿‡ `activated` ä¿¡å·æ•è·ã€‚
- æ·±è‰²æ¨¡å¼åˆ‡æ¢æŒ‰é’®çš„ `toggled` ä¿¡å·ç”¨äºæ”¹å˜ `Adw.StyleManager.color_scheme`ã€‚
- ç³»ç»Ÿä¸»é¢˜å˜åŒ–é€šè¿‡ `style_manager.notify["dark"]` ä¿¡å·æ•è·ï¼Œå¹¶è§¦å‘ç»˜å›¾åŒºåŸŸé‡ç»˜ã€‚

## å®ç°ä»£ç 

```vala
#!/usr/bin/env -S vala --pkg=gtk4 --pkg=libadwaita-1 -X -lm -X -O2 -X -march=native -X -pipe

/**
 * Solar Angle Calculator Application.
 *
 * A libadwaita application that calculates and visualizes solar elevation angles
 * throughout the day for a given location and date. The application provides
 * an interactive interface for setting latitude, longitude, timezone, and date,
 * and displays a real-time chart of solar elevation angles with export capabilities.
 */
public class SolarAngleApp : Adw.Application {
    // Constants for solar angle calculations
    private const double DEG2RAD = Math.PI / 180.0;
    private const double RAD2DEG = 180.0 / Math.PI;
    private const int RESOLUTION_PER_MIN = 1440; // 1 sample per minute
    // Constants for margins in the drawing area
    private const int MARGIN_LEFT = 70;
    private const int MARGIN_RIGHT = 20;
    private const int MARGIN_TOP = 50;
    private const int MARGIN_BOTTOM = 70;

    private Adw.ApplicationWindow window;
    private Gtk.DrawingArea drawing_area;
    private Gtk.Label click_info_label;
    private DateTime selected_date;
    private double sun_angles[RESOLUTION_PER_MIN];
    private double latitude = 0.0;
    private double longitude = 0.0;
    private double timezone_offset_hours = 0.0;
    private double clicked_time_hours = 0.0;
    private double corresponding_angle = 0.0;
    private bool has_click_point = false;

    // Color theme struct for chart drawing
    private struct ThemeColors {
        // Background colors
        double bg_r; double bg_g; double bg_b;
        // Grid colors with alpha
        double grid_r; double grid_g; double grid_b; double grid_a;
        // Axis colors
        double axis_r; double axis_g; double axis_b;
        // Text colors
        double text_r; double text_g; double text_b;
        // Curve colors
        double curve_r; double curve_g; double curve_b;
        // Shaded area colors with alpha
        double shade_r; double shade_g; double shade_b; double shade_a;
        // Click point colors
        double point_r; double point_g; double point_b;
        // Guide line colors with alpha
        double line_r; double line_g; double line_b; double line_a;
    }

    // Light theme color constants
    private ThemeColors LIGHT_THEME = {
        bg_r: 1.0, bg_g: 1.0, bg_b: 1.0,                    // White background
        grid_r: 0.5, grid_g: 0.5, grid_b: 0.5, grid_a: 0.5, // Gray grid
        axis_r: 0.0, axis_g: 0.0, axis_b: 0.0,              // Black axes
        text_r: 0.0, text_g: 0.0, text_b: 0.0,              // Black text
        curve_r: 1.0, curve_g: 0.0, curve_b: 0.0,           // Red curve
        shade_r: 0.7, shade_g: 0.7, shade_b: 0.7, shade_a: 0.3, // Light gray shade
        point_r: 0.0, point_g: 0.0, point_b: 1.0,           // Blue point
        line_r: 0.0, line_g: 0.0, line_b: 1.0, line_a: 0.5  // Blue guide lines
    };

    // Dark theme color constants
    private ThemeColors DARK_THEME = {
        bg_r: 0.0, bg_g: 0.0, bg_b: 0.0,                    // Black background
        grid_r: 0.5, grid_g: 0.5, grid_b: 0.5, grid_a: 0.5, // Light gray grid
        axis_r: 1.0, axis_g: 1.0, axis_b: 1.0,              // White axes
        text_r: 1.0, text_g: 1.0, text_b: 1.0,              // White text
        curve_r: 1.0, curve_g: 0.0, curve_b: 0.0,           // Bright red curve
        shade_r: 0.3, shade_g: 0.3, shade_b: 0.3, shade_a: 0.7, // Dark gray shade
        point_r: 0.3, point_g: 0.7, point_b: 1.0,           // Light blue point
        line_r: 0.3, line_g: 0.7, line_b: 1.0, line_a: 0.7  // Light blue guide lines
    };

    /**
     * Creates a new SolarAngleApp instance.
     *
     * Initializes the application with a unique application ID and sets
     * the selected date to the current local date.
     */
    public SolarAngleApp () {
        Object (application_id: "com.github.wszqkzqk.SolarAngleAdw");
        selected_date = new DateTime.now_local ();
    }

    /**
     * Activates the application and creates the main window.
     *
     * Sets up the user interface including input controls, drawing area,
     * and initializes the plot data with current settings.
     */
    protected override void activate () {
        window = new Adw.ApplicationWindow (this) {
            title = "Solar Angle Calculator",
        };
        
        // Create header bar
        var header_bar = new Adw.HeaderBar () {
            title_widget = new Adw.WindowTitle ("Solar Angle Calculator", ""),
        };

        // Add dark mode toggle button
        var dark_mode_button = new Gtk.ToggleButton () {
            icon_name = "weather-clear-night-symbolic",
            tooltip_text = "Toggle dark mode",
            active = style_manager.dark,
        };
        dark_mode_button.toggled.connect (() => {
            if (dark_mode_button.active) {
                style_manager.color_scheme = Adw.ColorScheme.FORCE_DARK;
            } else {
                style_manager.color_scheme = Adw.ColorScheme.FORCE_LIGHT;
            }
            drawing_area.queue_draw ();
        });

        // Listen for system theme changes
        style_manager.notify["dark"].connect (() => {
            drawing_area.queue_draw ();
        });

        header_bar.pack_end (dark_mode_button);

        // Create toolbar view to hold header bar and content
        var toolbar_view = new Adw.ToolbarView ();
        toolbar_view.add_top_bar (header_bar);

        var main_box = new Gtk.Box (Gtk.Orientation.HORIZONTAL, 0);

        var left_panel = new Gtk.Box (Gtk.Orientation.VERTICAL, 12) {
            hexpand = false,
            vexpand = true,
            width_request = 320,
            margin_start = 12,
            margin_end = 12,
            margin_top = 12,
            margin_bottom = 12,
        };

        // Location and Time Settings Group
        var location_time_group = new Adw.PreferencesGroup () {
            title = "Location and Time Settings",
        };

        var latitude_row = new Adw.SpinRow.with_range (-90, 90, 0.1) {
            title = "Latitude",
            subtitle = "Degrees",
            value = latitude,
            digits = 2,
        };
        latitude_row.notify["value"].connect (() => {
            latitude = latitude_row.value;
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        var longitude_row = new Adw.SpinRow.with_range (-180.0, 180.0, 0.1) {
            title = "Longitude",
            subtitle = "Degrees",
            value = longitude,
            digits = 2,
        };
        longitude_row.notify["value"].connect (() => {
            longitude = longitude_row.value;
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        var timezone_row = new Adw.SpinRow.with_range (-12.0, 14.0, 0.5) {
            title = "Timezone",
            subtitle = "Hours from UTC",
            value = timezone_offset_hours,
            digits = 2,
        };
        timezone_row.notify["value"].connect (() => {
            timezone_offset_hours = timezone_row.value;
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        location_time_group.add (latitude_row);
        location_time_group.add (longitude_row);
        location_time_group.add (timezone_row);

        // Date Selection Group
        var date_group = new Adw.PreferencesGroup () {
            title = "Date Selection",
        };

        var calendar = new Gtk.Calendar () {
            margin_start = 12,
            margin_end = 12,
            margin_top = 6,
            margin_bottom = 6,
        };
        calendar.day_selected.connect (() => {
            selected_date = calendar.get_date ();
            update_plot_data ();
            drawing_area.queue_draw ();
        });

        var calendar_row = new Adw.ActionRow ();
        calendar_row.child = calendar;
        date_group.add (calendar_row);

        // Export Group
        var export_group = new Adw.PreferencesGroup () {
            title = "Export",
        };

        var export_image_row = new Adw.ActionRow () {
            title = "Export Image",
            subtitle = "Save chart as PNG, SVG, or PDF",
            activatable = true,
        };
        var export_image_button = new Gtk.Button () {
            icon_name = "document-save-symbolic",
            valign = Gtk.Align.CENTER,
            css_classes = { "flat" },
        };
        export_image_button.clicked.connect (on_export_clicked);
        export_image_row.add_suffix (export_image_button);
        export_image_row.activated.connect (on_export_clicked);

        var export_csv_row = new Adw.ActionRow () {
            title = "Export CSV",
            subtitle = "Save data as CSV file",
            activatable = true,
        };
        var export_csv_button = new Gtk.Button () {
            icon_name = "x-office-spreadsheet-symbolic",
            valign = Gtk.Align.CENTER,
            css_classes = { "flat" },
        };
        export_csv_button.clicked.connect (on_export_csv_clicked);
        export_csv_row.add_suffix (export_csv_button);
        export_csv_row.activated.connect (on_export_csv_clicked);

        export_group.add (export_image_row);
        export_group.add (export_csv_row);

        // Click Info Group
        var click_info_group = new Adw.PreferencesGroup () {
            title = "Selected Point",
        };

        click_info_label = new Gtk.Label ("Click on chart to view data\n") {
            halign = Gtk.Align.START,
            margin_start = 12,
            margin_end = 12,
            margin_top = 6,
            margin_bottom = 6,
            wrap = true,
        };

        var click_info_row = new Adw.ActionRow ();
        click_info_row.child = click_info_label;
        click_info_group.add (click_info_row);

        left_panel.append (location_time_group);
        left_panel.append (date_group);
        left_panel.append (export_group);
        left_panel.append (click_info_group);

        drawing_area = new Gtk.DrawingArea () {
            hexpand = true,
            vexpand = true,
            width_request = 600,
            height_request = 500,
        };
        drawing_area.set_draw_func (draw_sun_angle_chart);

        // Add click event controller
        var click_controller = new Gtk.GestureClick ();
        click_controller.pressed.connect (on_chart_clicked);
        drawing_area.add_controller (click_controller);

        main_box.append (left_panel);
        main_box.append (drawing_area);

        toolbar_view.content = main_box;

        update_plot_data ();

        window.content = toolbar_view;
        window.present ();
    }

    /**
     * Calculates solar elevation angles for each minute of the day.
     *
     * @param latitude_rad Latitude in radians.
     * @param day_of_year Day of the year (1-365/366).
     * @param year The year.
     * @param longitude_deg Longitude in degrees.
     * @param timezone_offset_hrs Timezone offset from UTC in hours.
     */
    private void generate_sun_angles (double latitude_rad, int day_of_year, int year, double longitude_deg, double timezone_offset_hrs) {
        double sin_lat = Math.sin (latitude_rad);
        double cos_lat = Math.cos (latitude_rad);

        double days_in_year = ((year % 4 == 0) && ((year % 100 != 0) || (year % 400 == 0))) ? 366.0 : 365.0;

        for (int i = 0; i < RESOLUTION_PER_MIN; i += 1) {
            // fractional_day_component: day of year plus fraction of the day
            double fractional_day_component = day_of_year - 1 + ((double) i) / RESOLUTION_PER_MIN;
            // gamma: fractional year angle in radians
            double gamma_rad = (2.0 * Math.PI / days_in_year) * fractional_day_component;

            // Solar declination delta (rad) via Fourier series approximation
            double decl_rad = 0.006918
                - 0.399912 * Math.cos (gamma_rad)
                + 0.070257 * Math.sin (gamma_rad)
                - 0.006758 * Math.cos (2.0 * gamma_rad)
                + 0.000907 * Math.sin (2.0 * gamma_rad)
                - 0.002697 * Math.cos (3.0 * gamma_rad)
                + 0.001480 * Math.sin (3.0 * gamma_rad);

            // Equation of Time (EoT) in minutes
            double eqtime_minutes = 229.18 * (0.000075
                + 0.001868 * Math.cos (gamma_rad)
                - 0.032077 * Math.sin (gamma_rad)
                - 0.014615 * Math.cos (2.0 * gamma_rad)
                - 0.040849 * Math.sin (2.0 * gamma_rad));

            // True Solar Time (TST) in minutes, correcting local clock by EoT and longitude
            double tst_minutes = i + eqtime_minutes + 4.0 * longitude_deg - 60.0 * timezone_offset_hrs;

            // Hour angle H (Â°) relative to solar noon
            double ha_deg = tst_minutes / 4.0 - 180.0;
            double ha_rad = ha_deg * DEG2RAD;

            // cos(phi): cosine of zenith angle via spherical trig
            double cos_phi = sin_lat * Math.sin (decl_rad) + cos_lat * Math.cos (decl_rad) * Math.cos (ha_rad);
            // clamp to valid range
            if (cos_phi > 1.0) cos_phi = 1.0;
            if (cos_phi < -1.0) cos_phi = -1.0;
            // Zenith angle phi (rad)
            double phi_rad = Math.acos (cos_phi);

            // Solar elevation alpha = 90Â° - phi, convert to degrees
            double solar_elevation_rad = Math.PI / 2.0 - phi_rad;
            sun_angles[i] = solar_elevation_rad * RAD2DEG;
        }
    }

    /**
     * Updates solar angle data for current settings.
     */
    private void update_plot_data () {
        int day_of_year = selected_date.get_day_of_year ();
        double latitude_rad = latitude * DEG2RAD;
        int year = selected_date.get_year ();
        generate_sun_angles (latitude_rad, day_of_year, year, longitude, timezone_offset_hours);
        
        // Clear click point when data updates
        has_click_point = false;
        click_info_label.label = "Click on chart to view data\n";
    }

    /**
     * Handles mouse click events on the chart.
     *
     * @param n_press Number of button presses.
     * @param x X coordinate of the click.
     * @param y Y coordinate of the click.
     */
    private void on_chart_clicked (int n_press, double x, double y) {
        int width = drawing_area.get_width ();
        int height = drawing_area.get_height ();

        int chart_width = width - MARGIN_LEFT - MARGIN_RIGHT;

        // Check if click is within plot area and single click
        if (x >= MARGIN_LEFT && x <= width - MARGIN_RIGHT && y >= MARGIN_TOP && y <= height - MARGIN_BOTTOM && n_press == 1) {
            // Convert coordinates to time and get corresponding angle
            clicked_time_hours = (x - MARGIN_LEFT) / chart_width * 24.0;
            int time_minutes = (int) (clicked_time_hours * 60) % RESOLUTION_PER_MIN;
            corresponding_angle = sun_angles[time_minutes];
            has_click_point = true;

            // Format time display
            int hours = (int) clicked_time_hours;
            int minutes = (int) ((clicked_time_hours - hours) * 60);

            // Update info label
            string info_text = "Time: %02d:%02d\nSolar Elevation: %.1fÂ°".printf (
                hours, minutes, corresponding_angle
            );

            click_info_label.label = info_text;
            drawing_area.queue_draw ();
        } else {
            // Double click or outside plot area - clear point
            has_click_point = false;
            click_info_label.label = "Click on chart to view data\n";
            drawing_area.queue_draw ();
        }
    }

    /**
     * Draws the solar elevation chart.
     *
     * @param area The drawing area widget.
     * @param cr The Cairo context for drawing.
     * @param width The width of the drawing area.
     * @param height The height of the drawing area.
     */
    private void draw_sun_angle_chart (Gtk.DrawingArea area, Cairo.Context cr, int width, int height) {
        ThemeColors colors = style_manager.dark ? DARK_THEME : LIGHT_THEME;

        // Fill background
        cr.set_source_rgb (colors.bg_r, colors.bg_g, colors.bg_b);
        cr.paint ();

        int chart_width = width - MARGIN_LEFT - MARGIN_RIGHT;
        int chart_height = height - MARGIN_TOP - MARGIN_BOTTOM;

        double horizon_y = MARGIN_TOP + chart_height * 0.5; // 0Â° is at middle of -90Â° to +90Â° range
        
        // Shade area below horizon
        cr.set_source_rgba (colors.shade_r, colors.shade_g, colors.shade_b, colors.shade_a);
        cr.rectangle (MARGIN_LEFT, horizon_y, chart_width, height - MARGIN_BOTTOM - horizon_y);
        cr.fill ();

        // Draw horizontal grid every 15Â°
        cr.set_source_rgba (colors.grid_r, colors.grid_g, colors.grid_b, colors.grid_a);
        cr.set_line_width (1);
        for (int angle = -90; angle <= 90; angle += 15) {
            double tick_y = MARGIN_TOP + chart_height * (90 - angle) / 180.0;
            cr.move_to (MARGIN_LEFT, tick_y);
            cr.line_to (width - MARGIN_RIGHT, tick_y);
            cr.stroke ();
        }
        // Draw vertical grid every 2 hours
        for (int h = 0; h <= 24; h += 2) {
            double tick_x = MARGIN_LEFT + chart_width * (h / 24.0);
            cr.move_to (tick_x, MARGIN_TOP);
            cr.line_to (tick_x, height - MARGIN_BOTTOM);
            cr.stroke ();
        }

        // Draw axes and horizon
        cr.set_source_rgb (colors.axis_r, colors.axis_g, colors.axis_b);
        cr.set_line_width (2);
        cr.move_to (MARGIN_LEFT, height - MARGIN_BOTTOM);
        cr.line_to (width - MARGIN_RIGHT, height - MARGIN_BOTTOM);
        cr.stroke ();
        cr.move_to (MARGIN_LEFT, MARGIN_TOP);
        cr.line_to (MARGIN_LEFT, height - MARGIN_BOTTOM);
        cr.stroke ();
        // Horizon line
        cr.move_to (MARGIN_LEFT, horizon_y);
        cr.line_to (width - MARGIN_RIGHT, horizon_y);
        cr.stroke ();

        // Draw axis ticks and labels
        cr.set_source_rgb (colors.text_r, colors.text_g, colors.text_b);
        cr.set_line_width (1);
        cr.set_font_size (20);
        for (int angle = -90; angle <= 90; angle += 15) {
            double tick_y = MARGIN_TOP + chart_height * (90 - angle) / 180.0;
            cr.move_to (MARGIN_LEFT - 5, tick_y);
            cr.line_to (MARGIN_LEFT, tick_y);
            cr.stroke ();
            var te = Cairo.TextExtents ();
            var txt = angle.to_string ();
            cr.text_extents (txt, out te);
            cr.move_to (MARGIN_LEFT - 10 - te.width, tick_y + te.height / 2);
            cr.show_text (txt);
        }
        for (int h = 0; h <= 24; h += 2) {
            double tick_x = MARGIN_LEFT + chart_width * (h / 24.0);
            cr.move_to (tick_x, height - MARGIN_BOTTOM);
            cr.line_to (tick_x, height - MARGIN_BOTTOM + 5);
            cr.stroke ();
            var te = Cairo.TextExtents ();
            var txt = h.to_string ();
            cr.text_extents (txt, out te);
            cr.move_to (tick_x - te.width / 2, height - MARGIN_BOTTOM + 25);
            cr.show_text (txt);
        }

        // Plot solar elevation curve
        cr.set_source_rgb (colors.curve_r, colors.curve_g, colors.curve_b);
        cr.set_line_width (2);
        for (int i = 0; i < RESOLUTION_PER_MIN; i += 1) {
            double x = MARGIN_LEFT + chart_width * (i / (double) (RESOLUTION_PER_MIN - 1));
            double y = MARGIN_TOP + chart_height * (90.0 - sun_angles[i]) / 180.0;
            if (i == 0) {
                cr.move_to (x, y);
            } else {
                cr.line_to (x, y);
            }
        }
        cr.stroke ();

        // Draw click point if exists
        if (has_click_point) {
            // Calculate current coordinates from stored time and angle
            double clicked_x = MARGIN_LEFT + chart_width * (clicked_time_hours / 24.0);
            double corresponding_y = MARGIN_TOP + chart_height * (90.0 - corresponding_angle) / 180.0;

            cr.set_source_rgba (colors.point_r, colors.point_g, colors.point_b, 0.8);
            cr.arc (clicked_x, corresponding_y, 5, 0, 2 * Math.PI);
            cr.fill ();
            
            // Draw vertical line to show time
            cr.set_source_rgba (colors.line_r, colors.line_g, colors.line_b, colors.line_a);
            cr.set_line_width (1);
            cr.move_to (clicked_x, MARGIN_TOP);
            cr.line_to (clicked_x, height - MARGIN_BOTTOM);
            cr.stroke ();
            
            // Draw horizontal line to show angle
            cr.move_to (MARGIN_LEFT, corresponding_y);
            cr.line_to (width - MARGIN_RIGHT, corresponding_y);
            cr.stroke ();
        }

        // Draw axis titles
        cr.set_source_rgb (colors.text_r, colors.text_g, colors.text_b);
        cr.set_font_size (20);
        string x_title = "Time (Hour)";
        Cairo.TextExtents x_ext;
        cr.text_extents (x_title, out x_ext);
        cr.move_to ((double) width / 2 - x_ext.width / 2, height - MARGIN_BOTTOM + 55);
        cr.show_text (x_title);
        string y_title = "Solar Elevation (Â°)";
        Cairo.TextExtents y_ext;
        cr.text_extents (y_title, out y_ext);
        cr.save ();
        cr.translate (MARGIN_LEFT - 45, (double)height / 2);
        cr.rotate (-Math.PI / 2);
        cr.move_to (-y_ext.width / 2, 0);
        cr.show_text (y_title);
        cr.restore ();

        // Draw chart captions
        string caption_line1 = "Solar Elevation Angle - Date: %s".printf (selected_date.format ("%Y-%m-%d"));
        string caption_line2 = "Lat: %.2fÂ°, Lon: %.2fÂ°, TZ: UTC%+.2f".printf (latitude, longitude, timezone_offset_hours);
        
        cr.set_font_size (18);
        Cairo.TextExtents cap_ext1, cap_ext2;
        cr.text_extents (caption_line1, out cap_ext1);
        cr.text_extents (caption_line2, out cap_ext2);

        double total_caption_height = cap_ext1.height + cap_ext2.height + 5;

        cr.move_to ((width - cap_ext1.width) / 2, (MARGIN_TOP - total_caption_height) / 2 + cap_ext1.height);
        cr.show_text (caption_line1);
        cr.move_to ((width - cap_ext2.width) / 2, (MARGIN_TOP - total_caption_height) / 2 + cap_ext1.height + 5 + cap_ext2.height);
        cr.show_text (caption_line2);
    }

    /**
     * Handles export button click event.
     *
     * Shows a file save dialog with filters for PNG, SVG, and PDF formats.
     */
    private void on_export_clicked () {
        // Show save dialog with PNG, SVG, PDF filters
        var png_filter = new Gtk.FileFilter ();
        png_filter.name = "PNG Images";
        png_filter.add_mime_type ("image/png");
        
        var svg_filter = new Gtk.FileFilter ();
        svg_filter.name = "SVG Images";
        svg_filter.add_mime_type ("image/svg+xml");

        var pdf_filter = new Gtk.FileFilter ();
        pdf_filter.name = "PDF Documents";
        pdf_filter.add_mime_type ("application/pdf");

        var filter_list = new ListStore (typeof (Gtk.FileFilter));
        filter_list.append (png_filter);
        filter_list.append (svg_filter);
        filter_list.append (pdf_filter);

        var file_dialog = new Gtk.FileDialog () {
            modal = true,
            initial_name = "solar_elevation_chart.png",
            filters = filter_list,
        };

        file_dialog.save.begin (window, null, (obj, res) => {
            try {
                var file = file_dialog.save.end (res);
                if (file != null) {
                    export_chart (file);
                }
            } catch (Error e) {
                message ("Image file has not been saved: %s", e.message);
            }
        });
    }

    /**
     * Exports the current chart to a file.
     *
     * Supports PNG, SVG, and PDF formats based on file extension.
     * Defaults to PNG if extension is not recognized.
     *
     * @param file The file to export the chart to.
     */
    private void export_chart (File file) {
        int width = drawing_area.get_width ();
        int height = drawing_area.get_height ();

        if (width <= 0 || height <= 0) {
            width = 800;
            height = 600;
        }

        string filepath = file.get_path ();
        string? extension = null;
        var last_dot = filepath.last_index_of_char ('.');
        if (last_dot != -1) {
            extension = filepath[last_dot:].down ();
        }

        if (extension == ".svg") {
            Cairo.SvgSurface surface = new Cairo.SvgSurface (filepath, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_sun_angle_chart (drawing_area, cr, width, height);
        } else if (extension == ".pdf") {
            Cairo.PdfSurface surface = new Cairo.PdfSurface (filepath, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_sun_angle_chart (drawing_area, cr, width, height);
        } else {
            Cairo.ImageSurface surface = new Cairo.ImageSurface (Cairo.Format.RGB24, width, height);
            Cairo.Context cr = new Cairo.Context (surface);
            draw_sun_angle_chart (drawing_area, cr, width, height);
            surface.write_to_png (filepath);
        }
    }

    /**
     * Handles CSV export button click event.
     *
     * Shows a file save dialog for CSV format.
     */
    private void on_export_csv_clicked () {
        var csv_filter = new Gtk.FileFilter ();
        csv_filter.name = "CSV Files";
        csv_filter.add_mime_type ("text/csv");

        var filter_list = new ListStore (typeof (Gtk.FileFilter));
        filter_list.append (csv_filter);

        var file_dialog = new Gtk.FileDialog () {
            modal = true,
            initial_name = "solar_elevation_data.csv",
            filters = filter_list,
        };

        file_dialog.save.begin (window, null, (obj, res) => {
            try {
                var file = file_dialog.save.end (res);
                if (file != null) {
                    export_csv_data (file);
                }
            } catch (Error e) {
                message ("CSV file has not been saved: %s", e.message);
            }
        });
    }

    /**
     * Exports the solar elevation data to a CSV file.
     *
     * @param file The file to export the data to.
     */
    private void export_csv_data (File file) {
        try {
            var stream = file.replace (null, false, FileCreateFlags.REPLACE_DESTINATION);
            var data_stream = new DataOutputStream (stream);

            // Write CSV metadata as comments
            data_stream.put_string ("# Solar Elevation Data\n");
            data_stream.put_string ("# Date: %s\n".printf (selected_date.format ("%Y-%m-%d")));
            data_stream.put_string ("# Latitude: %.2f degrees\n".printf (latitude));
            data_stream.put_string ("# Longitude: %.2f degrees\n".printf (longitude));
            data_stream.put_string ("# Timezone: UTC%+.2f\n".printf (timezone_offset_hours));
            data_stream.put_string ("#\n");

            // Write CSV header
            data_stream.put_string ("Time,Solar Elevation (degrees)\n");

            // Write data points
            for (int i = 0; i < RESOLUTION_PER_MIN; i += 1) {
                int hours = i / 60;
                int minutes = i % 60;
                data_stream.put_string (
                    "%02d:%02d,%.3f\n".printf (hours, minutes, sun_angles[i])
                );
            }

            data_stream.close ();
        } catch (Error e) {
            message ("Error saving CSV file: %s", e.message);
        }
    }

    /**
     * Application entry point.
     *
     * Creates and runs the SolarAngleApp instance.
     *
     * @param args Command line arguments.
     * @return Exit code.
     */
    public static int main (string[] args) {
        var app = new SolarAngleApp ();
        return app.run (args);
    }
}
```

## ç¼–è¯‘ä¸è¿è¡Œè¯´æ˜
- æ–‡ä»¶æœ€ä¸Šæ–¹çš„ shebang æŒ‡å®šäº†ç¼–è¯‘å‘½ä»¤ï¼Œé™¤äº† GTK4 å’Œæ•°å­¦åº“å¤–ï¼Œè¿˜éœ€è¦é“¾æ¥ Libadwaita åº“ï¼š
  - `#!/usr/bin/env -S vala --pkg=gtk4 --pkg=libadwaita-1 -X -lm -X -O2 -X -march=native -X -pipe`
- è¿è¡Œå‰è¯·ç¡®ä¿ç³»ç»Ÿå·²å®‰è£… Valaã€GTK4ã€Libadwaita 1 (`libadwaita-1-dev` æˆ–ç±»ä¼¼åŒ…å) ä»¥åŠç›¸å…³ç¼–è¯‘å·¥å…·ã€‚
  - åœ¨ Arch Linux ä¸Šï¼Œå¯ä»¥ä½¿ç”¨ä»¥ä¸‹å‘½ä»¤å®‰è£…æ‰€éœ€çš„åŒ…ï¼š
    ```bash
    sudo pacman -S --needed vala gtk4 libadwaita
    ```
  - Windows ç”¨æˆ·å¯ä»¥ä½¿ç”¨ MSYS2 ç¯å¢ƒï¼Œå®‰è£…ç›¸å…³åŒ…ï¼š
    ```bash
    pacman -S --needed mingw-w64-ucrt-x86_64-vala mingw-w64-ucrt-x86_64-gcc mingw-w64-ucrt-x86_64-gtk4 mingw-w64-ucrt-x86_64-libadwaita
    ```
- å¯ä»¥ç›´æ¥æ‰§è¡Œè¯¥è„šæœ¬ï¼ˆå¦‚æœå·²èµ‹äºˆæ‰§è¡Œæƒé™ `chmod +x solarangleadw.vala`ï¼‰ï¼Œä¹Ÿå¯ä»¥åœ¨å‘½ä»¤è¡Œä¸­ä½¿ç”¨å¦‚ä¸‹å‘½ä»¤è¿›è¡Œç¼–è¯‘ï¼ˆå‡è®¾æ–‡ä»¶åä¿å­˜ä¸º `solarangleadw.vala`ï¼‰ï¼š
  ```bash
  valac --pkg=gtk4 --pkg=libadwaita-1 -X -lm -X -pipe -X -O2 solarangleadw.vala -o solarangleadw
  ```
  ç¼–è¯‘åå°†ç”Ÿæˆ `solarangleadw` å¯æ‰§è¡Œæ–‡ä»¶ï¼Œç›´æ¥è¿è¡Œå³å¯ã€‚

é€šè¿‡è¿™ä¸ª Libadwaita ç‰ˆæœ¬çš„ç¤ºä¾‹ï¼Œæˆ‘ä»¬å¯ä»¥çœ‹åˆ°å¦‚ä½•åˆ©ç”¨å…¶æä¾›çš„ç»„ä»¶å’Œç‰¹æ€§æ¥æ„å»ºå…·æœ‰ç°ä»£ GNOME å¤–è§‚å’Œæ„Ÿè§‰çš„åº”ç”¨ç¨‹åºï¼ŒåŒæ—¶è¿˜èƒ½æ–¹ä¾¿åœ°å®ç°å¦‚æ·±è‰²æ¨¡å¼é€‚é…ç­‰é«˜çº§åŠŸèƒ½ã€‚æ ¸å¿ƒä¸šåŠ¡é€»è¾‘ä¿æŒä¸å˜ï¼ŒUI/UX åˆ™é€šè¿‡ Libadwaita å¾—åˆ°äº†æ˜¾è‘—æå‡ã€‚
