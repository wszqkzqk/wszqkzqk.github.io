---
layout:     post
title:      探索Vala语言
subtitle:   Vala语言学习心得
date:       2022-10-17
author:     星外之神
header-img: img/Vala_Logo.webp
catalog:    true
tags:       GTK Vala 开源软件
---

***未经特殊说明，本文中的所有代码均采用[LGPL v2.1+](https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html)协议公开***

## 简介

Vala语言是一门专门为GObject对象设计的编程语言，语法类似于C#。Vala并没有自己的运行时，而是在编译时由Vala编译器将Vala源代码转化为C源代码，仅仅依赖C语言的基本特性，实现了现代语言的类型推断、`lambda`、`class`等各种高级功能。通常来说，基础的Vala程序的依赖仅有GLib和系统的C语言库，十分小巧精炼。Vala还具有自动的内存管理功能，它在编译时进行引用计数，生成的C代码与手动管理内存的C代码结构上类似，避免了引入GC或进行运行时引用计数检查对程序效率的额外开销。因此，Vala同时拥有C#的高开发效率与C语言的高运行效率和低内存占用。

此外，Vala语言还具有跨平台的特性。Vala背靠完备的基础库GLib，强大的图形库GTK，高度集成的媒体库GStreamer，以及SDL2、ZLib、OpenGL等各种各样的非GNOME库语言绑定，还可以通过vapi进一步拓展。Vala既能用来开发高效率运行的CLI应用，又能用来开发功能丰富的GUI应用，适用场景较广。

附上与Vala相关的重要网站：
* [**Vala Programming Language**](https://vala.dev/)
  * Vala语言官网，包含Vala语言的发布信息与基本介绍
* [**Valadoc.org**](https://valadoc.org/)
  * Vala文档网站，包含了Vala及Vala的语言绑定库的大量文档，对开发与学习很有帮助
* [**GNOME: Projects/Vala/Documentation**](https://wiki.gnome.org/Projects/Vala/Documentation)
  * GNOME官方对Vala文档的汇总
* [**GNOME: Projects/Vala/Tutorial**](https://wiki.gnome.org/Projects/Vala/Tutorial)
  * GNOME官方的Vala教程
* [**Vala Reference Manual**](https://www.vala-project.org/doc/vala/)
  * GNOME官方的Vala语言参考手册
* [**Introducing Vala Programming**](https://link.springer.com/book/10.1007/978-1-4842-5380-9)
  * 实体书，Vala编程介绍
* [**elementary OS: Developer Docs**](https://docs.elementary.io/develop/)
  * elementary OS的开发文档
* [**Meson With Vala**](https://mesonbuild.com/Vala.html)
  * 使用Meson构建系统组织构建Vala程序
* [**Vala for C# Programmers**](https://wiki.gnome.org/Projects/Vala/ValaForCSharpProgrammers)
  * 面向C#程序员的Vala教程
* [**Vala for Java Programmers**](https://wiki.gnome.org/Projects/Vala/ValaForJavaProgrammers)
  * 面向Java程序员的Vala教程
* [**The GTK project**](https://www.gtk.org/)
  * GTK计划官网，内含GTK相关信息、资源、文档，也包含GTK的Vala语言绑定的相关内容
* [**GTK Documentation**](https://docs.gtk.org/)
  * GTK文档网站，内含GTK相关的C语言文档，可供Vala语言开发参考

## 优势

### 语法

Vala语法与C#非常类似，由于Vala语言相对而言不那么大众，很多时候学习Vala语法可以直接参考C#的资料。

笔者在这里列出了一些Vala代码语法的特别之处，简要示出Vala语言的优势。

#### 变量类型命名

Vala对整数变量类型的命名较为简洁，没有使用啰嗦冗长的`unsigned long long`这样的形式。Vala中`char`、`int`、`long`等拼写简单的类型名仍然不变，但是对两个单词及以上的类型名进行了调整，仅简单指出其符号与位数来表示，如：没有`usigned int`，而是使用`uint`；没有`long long`，而是使用`int64`；没有`usigned long long`，而是使用`uint64`。这使得代码更加简洁，既方便编写，又方便阅读。

此外，Vala给各种长度的整数都给出了跨平台的保证位数的实现，例如：`long`在64位Linux下为64位，但在64位Windows下为32位，这时如果有必要可以考虑使用保证长度的`int64`或`int32`实现。

#### 匿名函数lambda

Vala的`lambda`匿名函数语法特别简洁，基本结构为`params => body`，具体形式如下：
```vala
([arg1], [arg2], [...]) => statement
([arg1], [arg2], [...]) => {block}
```

前面的小括号内传递参数，不需要声明类型。后面的语句直接给出函数内容，同样不需要声明返回值的类型。

Vala的`lambda`匿名函数使用便捷，功能也比Python中仅相当于`return`语句的`lambda`函数更强大。在很多传参时需要给出一个函数的地方（例如信号的`connect`）使用特别方便。

#### `main`函数的灵活性

C#要求将`Main`函数置于一个`class`内，而Vala的`main`函数既可以在`class`内，又可以在`class`外，必要时将Vala当作是简化的C语言来用也未尝不可。

#### 指针适度使用

由于Vala语言具有自动内存管理功能，一般不需要手动管理指针，例如，与C++对比，Vala的`foreach`循环不需要手动包含取地址的操作：
```vala
void main () {
    int[] nums = {1, 2, 3, 4, 5};
    foreach (var i in nums) {
        print ("%d\n", i);
    }
    // Output:
    // 1
    // 2
    // 3
    // 4
    // 5
}
```

Vala的中的`List`等容器也不需要手动指针清理，在离开其有效语段或手动将其赋值为`null`后即可自动释放。

同时，Vala又与C#不同，Vala仍可以手动在任何地方使用指针，不需要特别包括在`unsafe{}`语段中，例如：
```vala
uint int64_hash (double? v) {
    return (uint) (((*(uint64*) v >> 32)) ^ (*(uint64*) v & 0xffffffff));
}
```

这里定义了一个双精度浮点数的hash函数，为了避免将浮点直接转化为整数时导致的溢出或整数部分相同时的剧烈hash碰撞等种种问题，此处将double的前32位与后32位取异或，得到hash值。这一过程需要在存储double的内存空间中取出两个整数，不可避免地需要使用指针。

但即使如此，由于需要指针的常见数据结构在Vala的标准库GLib以及容器库Gee中已有较为完备的实现，Vala中极少需要手动使用指针，Vala官方也建议谨慎手动使用。

#### 语法糖设计

Vala中还有很多的语法糖，可以简化语句并提高开发效率。例如：
```vala
void main () {
    var map = new Gee.HashMap<string, int> ();

    map.set ("one", 1);
    map["two"] = 2;             // The same as map.set ("two", 2)

    var a = map.get ("one");
    var b = map["two"];        // The same as map.get ("two")

    if (map.has_key ("one")) {
        print ("\"one\" is in map.keys");
    }

    if ("two" in map.keys) {         // The same as map.has_key ("two")
        print ("\"two\" is in map.keys");
    }
}
```

### 入门曲线

笔者认为Vala语言的学习曲线明显较C或者C++平缓。一方面，Vala的语法较C与C++简单，易于上手；另一方面，Vala具有自动化的内存管理功能，不用手动管理内存，甚至由于GLib与Gee内置的数据结构极其丰富，也基本上可以不用指针。Vala还不需要C或者C++那样的`include`语句，对于基础的编程内置的GLib库完全足够，不需要知道调用其他库的命令，这对入门也十分友好；而想要在Vala中使用其他库时也十分简单，不需要使用预处理语句或者复杂的链接命令，只需要在编译参数或者Unix Shebang中用`--pkg=xxx-x.x`指明所需要的库即可。此外，由于Vala有强大的标准库，很多在C或者C++中需要自己慢慢实现的函数或功能在GLib库中已有集成，同样也降低了初学者的学习负担。

事实上，笔者虽然自初中以来一直都有学习Vala的想法，但一直没有真正去尝试，直到在学了Python以后自学C/C++受阻（只会Python的菜鸡刚刚接触这样较底层的语言，再加上是较混乱的自学，没有头绪，当时确实感觉有点困难🤣🤣🤣），才重新想起Vala语言，这才开始学习Vala。这也许也可以作为Vala学习曲线较C/C++平缓的一个论据😂。

### 标准库与功能

Vala的设计即为现代化的高级语言，拥有大量现代语言的功能。此外，Linux平台下使用最广泛、功能集成最丰富的C语言库——GLib可以认为是Vala语言的标准库。GLib内置了极其丰富的内置函数、GObject对象、I/O通道、数据结构，因此，Vala语言内置的标准库也十分强大。下面笔者将对一些自己喜欢的Vala内置的强大功能进行简要介绍。

#### 字符串处理及输入输出

笔者非常喜欢Vala强大的字符串处理功能。

Vala支持直接用`+`拼接字符串：
```vala
void main () {
    var s1 = "123";
    var s2 = "456";
    var s3 = s1 + s2 + "\n";
    print (s3);
    // Output:
    // 123456
}
```

对于一般的字符串处理，除了C语言风格的`xxx.printf ()`处理外，Vala还支持`@"content"`形式的模板字符串，在模板字符串中，可以用`$xxx`或`$(xxx)`插入所需要输出的内容，可以是变量，也可以是一个运算语句，例如：
```vala
void main () {
    int n = 123456;
    uint m = 987654321;
    double pi = 3.141592653589;
    print (@"$m + $n + pi = $(m + n + pi)\n");
    // Output:
    // 987654321 + 123456 + pi = 987777780.14159262
}
```

使用模板字符串进行输出，既不用像C语言风格的`printf`一样在字符串中用`%`声明变量类型，又不用像C++的`cout`一样用很长一串`<<`连接。Vala的模板字符串是先完整生成，再整体输出，也避免了`cout`的线程不安全问题，是一种方便又不乏灵活的方法。另外，模板字符串可以在生成后存储待用而不输出，整体来看是一种与Python语言的`f-string`十分相似的方法，便于使用。

当然，Vala也支持C语言风格的printf格式化，效果与之前的模板字符串实现类似，但是更加麻烦：
```vala
void main () {
    int n = 123456;
    uint m = 987654321;
    double pi = 3.141592653589;
    print ("%u + %d + pi = %f\n", m, n, pi + m + n);
    // Output:
    // 987654321 + 123456 + pi = 987777780.141593
}
```

Vala的模板字符串中`$`的使用类似于Unix Shell的变量引用，符合众多Linux用户的使用习惯，同时功能强大，很多时候是简化输出代码的一大利器。

除此之外，Vala还支持字符串切片、字符串中字符的提取，例如：
```vala
void main () {
    var s1 = "abcdefg";
    var s2 = s1[0:3];
    var c = s1[3];
    print (@"$s2\n$c\n");
    // Output:
    // abc
    // d
} 
```

Python程序员注意：这里提取的单个字符数据类型是`char`而不是`string`，`char`在代码中表示时需要用单引号，而`string`需要用双引号。`char`本质上是一个8位有符号数字，可以直接与数字进行比较，不需要Python的`ord`函数。

Vala还支持逐字字符串，类似于Python的`r-string`或C#的`@""`（然而C#的逐字字符串的语法在Vala中表示字符串模板），形如`"""xxx"""`（注意：Python中也有类似语法表达，但是意义为多行字符串；Vala语言与Python不同，不依赖换行符作为语句分隔，因此各种表达方式的字符串都能跨行，这里的`"""xxx"""`还特别表示了逐字字符串）

Vala的文字输入也较为方便。Vala除了可以使用C语言风格的`getc`、`scanf`等外，还有`read_line`函数可以读取一整行的内容，注意使用时是通过`FileStream`对象下的方法来进行的（`getc`、`scanf`函数也是如此），如果要从标准输入流读取，即使用`stdin.read_line()`：
```vala
void main () {
    var s = stdin.read_line ();
    print (@"Your input is: $s\n");
    // Output:
    // Your output is: xxx(your input)
}
```

这一函数功能上类似于Python等语言的`input`，对熟悉Python的人来说比较方便。

由于Vala背靠强大的GLib库，对字符串的高级处理也有很好的支持。诸如`split`、`replace`、`has_prefix`、`has_suffix`、`ascii_down`、`ascii_up`、`strip`等较高级的处理函数在内置的`string`对象中均有实现，可以方便地实现字符串的高级处理。以下是示例：
```vala
void main () {
    var s1 = "Hello, beautiful but dangerous world!";
    var tab = s1.split (" ");
    print ("%s\n%s\n", tab[0], tab[4]);
    // Output:
    // Hello,
    // World!

    var s2 = s1.ascii_up ();
    print ("%s\n", s2);
    // Output:
    // HELLO, BEAUTIFUL BUT DANGEROUS WORLD!

    var s3 = s1.replace ("beautiful but dangerous", "wonderful");
    print ("%s\n", s3);
    // Output:
    // Hello, wonderful world!

    var s4 = s1.reverse ();
    print ("%s\n", s4);
    // Output:
    // !dlrow suoregnad tub lufituaeb ,olleH

    var s5 = "  \t  Hello, world!  \t\t ";
    var s6 = s5.strip ();
    print ("%s\n", s6);
    // Output:  
    // Hello, world!

    var pre = "Hell";
    if (s6.has_prefix (pre)) {
        print (@"\"$pre\" is the prefix of \"$s6\"\n");
    }
    // Output:  
    // "Hell" is the prefix of "Hello, world!"
}
```

Python程序员需要注意，Vala中的`split`必须要手动指定分隔符，当分隔符指定为通常的空格时，行为将与Python中默认的无参`split()`不同，指定了`split(" ")`时，无论是在Python中还是在Vala中，多个连续空格间将会分隔出空字符串，而无参的`split()`则没有：
```python
tab = "1    2".split()
```

这样得到的`tab`内容为`["1", "2"]`。而对于Vala：
```vala
var tab = "1    2".split (" ");
```

这样得到的`tab`内容为`{"1", "", "", "", "2"}`。有时忘记处理空字符串会引发某些bug。

#### 正则表达式

Vala内置集成了强大的正则表达式功能，可以在代码中直接使用正则表达式，也可以将一般的字符串形式的正则表达式编译为正则表达式对象。正则表达式在Vala中与语言的集成度高，使用方便：
```vala
void main () {
    var msys2_dep_regex = new Regex (""".*(/|\\)(usr|ucrt64|mingw64|clang64|mingw32|clang32|clangarm64)(/|\\)""");
    var path = "D:\\msys64\\ucrt64\\bin\\libglib-2.0-0.dll";
    MatchInfo match_info;

    if (msys2_dep_regex.match (path, 0, out match_info)) {
        print (@"This file is in msys2 environment \"$(match_info.fetch(0))\".\n");
    } else {
        print ("This file doesn't in msys2 environment.\n");
    }
    // Output:
    // This file is in msys2 environment "D:\msys64\ucrt64\".
}
```

Vala语言还可以在编译时自动识别正则表达式内容，不用手动从字符串新建：
```vala
Regex quote_regex = /(".*")|('.*')/;
var msys2_dep_regex = /.*(\/|\\)(usr|ucrt64|clang64|mingw64|mingw32|clang32|clangarm64)(\/|\\)/;
```

直接的正则表达式对象不仅简化了语法，而且Vala会在编译时的时候检查正则表达式的正确性（LSP也会在IDE中指出错误），便于及早发现正则表达式中的错误，避免人为的疏忽进入程序中，增加了程序的稳健性。

### Vala Scripts与Unix Shebang

Vala语言支持将源代码**第一行**写为Unix Shebang的形式：
```vala
#!/usr/bin/env vala
```

赋予该源代码文件可执行权限后即可直接执行该未经编译的源代码。在执行时，系统先会自动调用`vala`命令（不是一般编译时用的`valac`命令）对代码文件进行编译，然后再自动执行编译得到的文件，整体上起到了类似脚本的效果。因此简单Vala的程序的调试十分方便，免去了手动编译的麻烦。这一特性也使得Vala可以当作脚本语言（即Vala Scipts）在类Unix系统下使用。由于在Linux下valac与gcc编译耗时都较短，Vala Script启动延迟低，而且运行的是编译好的本机二进制机器码，没有JIT或解释器的性能开销，运行效率高，运行效率高，有着不错的体验。

另外，我们也可以在Shebang中向编译器正常传入参数，例如，可以开启编译器优化：
```vala
#!/usr/bin/env -S vala -X -O2 -X -march=native -X -pipe
```

这里需要注意的是`env`的特性：需要传递`-S`参数表示后面的命令按照空格分隔。传给Vala编译器的命令中，`-X`后所紧跟的内容会直接传递给gcc编译器，这里的`-O2`表示按照`O2`等级启用编译器优化，`-march=native`表示合理、充分利用本机支持的所有指令集，`-pipe`表示让gcc生成的中间文件直接通过管道传递，不写入硬盘。这三个选项均是用来加快运行速度或者编译速度的。

由于Vala Scripts运行的时候存在自动编译的过程，如果安装了某些能够加速编译的软件，可以加入调用的命令参数中，可能会有一定程度上减小程序启动延迟的效果：
```vala
#!/usr/bin/env -S vala -X -O2 -X -march=native --cc="ccache gcc" -X -fuse-ld=mold -X -pipe
```

以上的Shebang参数调用了`ccache`进行编译缓存加速，并使用了先进、高效的`mold`链接器进行链接，可以加快编译速率。当然，这需要提前安装好所需要的`mold`和`ccache`才能正常运行。

我们还可以在Shebang中为Vala编译器指定所需要调用的库，例如：
```vala
#!/usr/bin/env -S vala --pkg=gee-0.8 --pkg=gio-2.0 --pkg=gtk+-3.0
```

这代表令Vala编译器寻找并调用`gee`、`gio`、`gtk+-3.0`这3个库。同时，这样的语句也可以被LSP识别，令LSP能够理解所调用的库中包含的内容。

***注意：这一功能虽然强大，但毕竟与Unix特性高度相关，仅在类Unix平台下可用，Windows下无法直接执行带有Shebang的文件，仅可用于LSP识别***

当然，作为"Scripts"的类似物，Vala还实验性支持顶级语句功能，即，像脚本语言一样生成该程序，不用将程序入口点放在`main ()`函数中：
```vala
#!/usr/bin/env -S vala -X -O2 -X -march=native -X -pipe

var a = 123;
var b = 456;
print (@"$a plus $b is $(a+b)");
```

以上Vala代码虽然没有`main`函数，但是也可以正常编译运行，或者在Unix下授予可执行权限执行直接执行。程序会将顶级语段视作程序入口点正常处理。

这些特性使得Vala虽然实际上是一个静态编译的语言，但也有着一些贴近脚本语言的方便特性。同时，高度简化的语法也降低了新手入门的门槛。

### IDE与LSP支持

目前，GNOME Builder与安装了Vala语言插件的VS Code都是非常易用的Vala开发环境，对于较轻量级的开发，KDE的高级文本编辑器Kate也是一个不错的选择。结合Vala language server或gvls的LSP功能，这些IDE或编辑器支持了语法高亮、自动补全、代码格式化、代码静态分析、定义跳转、文档集成、调试断点等强大功能，增加了开发效率。

## 不足

自Vala语言于2006年推出到现在，始终没有能够发布正式版。该语言虽然开发高效，使用便捷，但是使用人数与开发人数都远远小于主流大众语言。因此，尤其是在一些不太常用的地方，Vala可能会存在一定的编译器Bug。而这些编译器Bug在开发过程中往往难以定位，降低开发效率。Vala语言主要是为了GObject开发，而GObject相关程序在Linux下有着很广泛的使用，但是在Windows平台下用得并不多，所以Vala对Windows平台下的开发体验可能顾及较少。

笔者在这里对自己在学习Vala过程中遇到的编译器Bug或反人类之处、不合理之处予以列举。

### 反斜杠灾难：Windows下的编译器Bug（已修复）

Vala程序支持使用GDB等工具进行调试（需要在编译时向`valac`编译器传递`-g`参数）。Vala编译器依靠在生成的C源代码中插入`#line`语句来加入程序中对应的源代码文件的路径以及行数等调试信息。

然而，在笔者将这一Bug修复前，Vala会将编译参数中的文件路径未加处理即写入到生成的C源代码中。对于Linux等类Unix平台，文件路径的分隔符为`/`，一般不会出现问题。但是Windows平台的文件路径分隔符为`\`，C源代码字符串中的`\`将会被识别为转义字符，这会导致`#line`语句中的源代码文件路径错误，使得调试无法进行；如果在路径中`\`后面存在不可转义的字符，还会在编译的时候大量报错。

***笔者已向上游提交[补丁](https://gitlab.gnome.org/GNOME/vala/-/commit/541e9f2c9dfde40b38fce32b8deb695f3eaf080b)，该Bug于`0.56.3`修复***

### 头文件包含问题：GType的使用与GLib的包含（已修复）

Vala通常需要依赖GLib与GObject进行使用。默认情况下，任何Vala源代码都看作默认加入了一行`using GLib`，默认使用的基本数据类型（例如：`int`, `uint`, `int64`, `uint64`, `char`, `double`）都是GLib中定义的（对应上文的例子，即`gint`, `guint`, `gint64`, `guint64`, `gchar`, `gdouble`），默认包含的大量内置函数（例如`print`, `get_real_time`）也来自GLib（上文例子对应的是`g_print`, `g_get_real_time`）。甚至，Vala中的Bool值也来自于GLib中宏的定义。

另外，Vala语言本身并没有`#include`语句，Vala在C代码层面对头文件的使用由Vala编译器自动处理。这导致了有时即使没有显式使用GLib中的内容，也存在对GLib头文件的依赖，如果Vala编译器没有在生成的C代码中自动包含所需要的头文件，便会造成编译错误。

例如：
```vala
void main () {
    if (true) {
        stdout.printf ("test");
    }
}
```

将这段代码编译，或授予Unix可执行权限执行，会提示以下错误信息：
```bash
/tmp/temptest.vala.XXDIT1.c: In function '_vala_main':
/tmp/temptest.vala.XXDIT1.c:11:13: error: 'TRUE' undeclared (first use in this function)
   11 |         if (TRUE) {
      |             ^~~~
/tmp/temptest.vala.XXDIT1.c:11:13: note: each undeclared identifier is reported only once for each function it appears in
error: cc exited with status 256
Compilation failed: 1 error(s), 0 warning(s)
```

还有更加隐式的例子：
```vala
void main () {
    while (1 == 1) {
        stdout.printf ("test\n");
        break;
    }
}
```

同样会出现编译错误：
```bash
/tmp/1.vala.c: In function '_vala_main':
/tmp/1.vala.c:11:16: error: 'TRUE' undeclared (first use in this function)
   11 |         while (TRUE) {
      |                ^~~~
/tmp/1.vala.c:11:16: note: each undeclared identifier is reported only once for each function it appears in
error: cc exited with status 1
Compilation failed: 1 error(s), 0 warning(s)
```

这是因为Vala生成代码中的`TRUE`是在GLib的头文件中定义的，而这段程序没有明显地使用GLib的内容，Vala编译器并没有自动包含GLib的头文件，使得`TRUE`未定义，造成编译失败。

***笔者向上游[反馈了这一bug](https://gitlab.gnome.org/GNOME/vala/-/issues/1364)，该bug[已于目前的git存储库中修复](https://gitlab.gnome.org/GNOME/vala/-/commit/1560e367afc7d95ded40f1c67e559ac60c4c49cc)***

### 容器中对象的运算符问题：`a[i]+=1`、`a[i]=a[i]+1`与`a[i]++`的坑

这样一段简单的数字计数统计代码即可试出这一问题：
```vala
void main () {
    Intl.setlocale ();
    var nums = stdin.read_line ().split (" ");
    var dic = new Gee.HashMap<int64?, uint64?> ((Gee.HashDataFunc) int64_hash, (Gee.EqualDataFunc) int64_equal);
    foreach (var i in nums) {
        int64 num = int64.parse (i);
        if (num in dic.keys) {
            dic[num] = dic[num] + 1;
        } else {
            dic[num] = 1;
        }
    }
    foreach (var i in dic.values) {
        print (@"$i\n");
    }
}
```

以上代码可以正常编译运行。但注意到语句`dic[num] = dic[num] + 1`，若将其改为`dic[num]++`，将会在编译时报错：
```bash
error: The expression `Gee.HashMap<int64,uint64?>' does not denote an array
```

而改成`dic[num] += 1`后，该语句的实际效果变成了`dic[num] = 1`，在编译时传递`-C`参数，检查Vala编译器生成的C代码，对比正常结果与异常结果可以发现，改成`dic[num] += 1`后，Vala显然没有正确处理`+=`语句：
```
 					GeeHashMap* _tmp18_;
-					guint64 _tmp19_;
+					GeeHashMap* _tmp19_;
+					gpointer _tmp20_;
+					guint64* _tmp21_;
+					guint64 _tmp22_;
 					_tmp18_ = dic;
-					_tmp19_ = (guint64) 1;
-					gee_abstract_map_set ((GeeAbstractMap*) _tmp18_, &num, &_tmp19_);
+					_tmp19_ = dic;
+					_tmp20_ = gee_abstract_map_get ((GeeAbstractMap*) _tmp19_, &num);
+					_tmp21_ = (guint64*) _tmp20_;
+					_tmp22_ = (*_tmp21_) + 1;
+					gee_abstract_map_set ((GeeAbstractMap*) _tmp18_, &num, &_tmp22_);
+					_g_free0 (_tmp21_);
```

完整diff参见这里：[collectionBug.diff](https://gitlab.gnome.org/GNOME/vala/uploads/7ecfcb3276c99b8d45ffe85a3508432f/collectionBug.diff)

在使用通常的非容器中的变量时，Vala编译器均能正常处理`+=`与`++`操作。很明显，在这个例子中，Vala编译器处理容器中的内容时出现了Bug。

### 类型检查

Vala是强类型语言，在一定程度上保证类型安全的同时，很多本来完全可以隐式转化甚至直接等价的类型并不能隐式转化，因此也带来了一些不便。例如，Vala中针对GLib中数据结构的`GLib.HashFunc`与针对Gee中数据结构的`Gee.HashDataFunc`在C原因呢代码层面上来看本质上都是`uint`类型，然而在Vala程序中，要求传递一个`Gee.HashDataFunc`函数作为参数的地方并不能直接使用一个类型为`HashFunc`的函数，甚至也不能使用类型为`uint`的函数，如果要使用，需要在传参时显式强制类型转化，这样的规定并没有意义，还会为编程带来不便。

此外，Vala对部分`nullable`类型的适配也并不充分，内置的大量能够为`int`、`uint`、`double`等自动获取内置hash、equal、cmp函数数据结构下的方法在遇到`int?`、`uint?`、`double?`时通通失灵，因此对于`nullable`类型，这些函数还往往需要手动编写或指定，比较麻烦。

Vala在一些该进行类型检查的却又没有很好地进行类型检查。巧合的是，这样的Bug又是笔者在GLib的容器对象中观察到的，这可能是因为Vala语言中的开发主要用的是Gee中的容器对象，GLib中的容器使用较少，较易存在Bug。例如：
```vala
#!/usr/bin/env -S vala -X -O2 -X -march=native -X -pipe
void main () {
    var dic = new HashTable<string, double> (str_hash, str_equal);
    dic["pi"] = 3.141592653589793;
    dic["e"] = 2.718281828459045;
    dic.foreach ((key, value) => {
        print (@"$key --> $value\n");
    });
}
```

这里的泛型参数`double`并不受支持，需要使用`double?`令Vala正确自动处理指针。然而，Vala编译器并没有在此报错，报错发生在了C语言的编译阶段：
```
D:/17265/test.vala.c: In function '_vala_main':
D:/17265/test.vala.c:126:36: warning: passing argument 2 of 'g_hash_table_foreach' from incompatible pointer type [-Wincompatible-pointer-types]
  126 |         g_hash_table_foreach (dic, ___lambda4__gh_func, NULL);
      |                                    ^~~~~~~~~~~~~~~~~~~
      |                                    |
      |                                    void (*)(const void *, const void *, void *)
In file included from D:/msys64/ucrt64/include/glib-2.0/glib.h:52,
                 from D:/17265/test.vala.c:4:
D:/msys64/ucrt64/include/glib-2.0/glib/ghash.h:109:61: note: expected 'GHFunc' {aka 'void (*)(void *, void *, void *)'} but argument is of type 'void (*)(const void *, const void *, void *)'
  109 |                                             GHFunc          func,
      |
```

而使用`Gee.HashMap`时将会在Vala代码向C代码编译的阶段正常报错：
```vala
#!/usr/bin/env -S vala --pkg=gee-0.8 -X -O2 -X -march=native -X -pipe
void main () {
    var dic = new Gee.HashMap<string, double> ();
    dic["pi"] = 3.141592653589793;
    dic["e"] = 2.718281828459045;
    foreach (var i in dic) {
        print (@"$(i.key) --> $(i.value)\n");
    }
}
```

```
gee-0.8.vapi:195.122-195.122: error: `double' is not a supported generic type argument, use `?' to box value types
  195 |         public abstract class AbstractMap<K,V> : GLib.Object, Gee.Traversable<Gee.Map.Entry<K,V>>, Gee.Iterable<Gee.Map.Entry<K,V>>, Gee.Map<K,V> {
      |                                                                                                                       
          ^
gee-0.8.vapi:195.122-195.122: error: `double' is not a supported generic type argument, use `?' to box value types
  195 |         public abstract class AbstractMap<K,V> : GLib.Object, Gee.Traversable<Gee.Map.Entry<K,V>>, Gee.Iterable<Gee.Map.Entry<K,V>>, Gee.Map<K,V> {
      |                                                                                                                       
          ^
Compilation failed: 2 error(s), 0 warning(s)
```

### （个人习惯问题）从C#引入的“不能使用其他数据类型代替Bool”的合理性

C、C++、Python等大多数的语言一般都会支持用0与非0（或者空与非空）分别表示Bool值的假与真。然而，C#语言因为担心这可能会容易使程序员犯某些错误（如赋值`=`与等于`==`搞混），在条件判断中只支持使用显式的Bool值或Bool条件表达式。语法高度与C#相似的Vala也从C#那里继承来了这个特性。例如，`if (object) {}`在C语言或者C++中是正确的，在Vala中却不行，需要改成`if (object != 0) {}`或者`if (object != null)`。对笔者个人而言，这样的设计带来了不便。

### 部分功能的完备性

对于容器对象，`foreach`循环可以方便地遍历其中的内容，Vala也支持直接的`foreach`循环。对于继承了Gee中`Gee.Iterable`的对象，Vala能够自动适配其`foreach`循环。但是GLib中的对象并不会继承Gee中的这一接口，对于GLib中的`Array`、`List`等对象，需要Vala维护者人为在`glib-2.0.vapi`中实现`foreach`支持。但是，直到如今，Vala也没有为GLib中的所有容器对象实现直接的`foreach`循环，例如：`GLib.HashTable`目前不能直接使用`foreach`，而需要调用`HashTable`内置的`foreach`函数来实现`foreach`的功能：
```vala
void main () {
    var dic = new HashTable<string, double?>(str_hash, str_equal);
    dic["pi"] = 3.141592653589793;
    dic["e"] = 2.718281828459045;
    dic.foreach((key, value) => {
        print (@"$key --> $value\n");
    });
}
```

### LSP的稳定性

Vala的LSP虽然功能强大丰富，支持以vscode插件的形式非常方便地调用，但目前稳定性并不理想，尤其是在Windows平台下崩溃极其频繁，甚至到了难以使用的地步。

### 默认时没有充分使用彩色报错

Vala的报错提示在默认情况下通常不是彩色的，需要在编译时加入`--color=always`参数才能开启彩色的报错。

### 其他问题：产生的C代码的效率与可读性

Vala编译器在将Vala源代码编译为C源代码时，会产生很多冗余的`_tmpX_`变量，以及多余的变量间赋值操作，造成额外的性能开销。

此外目前Vala编译器似乎是为了兼容古老的C89，某些代码生成得十分复杂，例如这样一个简单的循环：
```vala
void main () {
    for (var i=0;i<10;i+=1) {
        print ("%d/n", i);
    }
}
```

由此编译得到的C代码为：
```c
/* tmp.c generated by valac 0.56.3, the Vala compiler
 * generated from tmp.vala, do not modify */

#include <glib.h>

static void _vala_main (void);

static void
_vala_main (void)
{
	{
		gint i = 0;
		i = 0;
		{
			gboolean _tmp0_ = FALSE;
			_tmp0_ = TRUE;
			while (TRUE) {
				if (!_tmp0_) {
					i += 1;
				}
				_tmp0_ = FALSE;
				if (!(i < 10)) {
					break;
				}
				g_print ("%d/n", i);
			}
		}
	}
}

int
main (int argc,
      char ** argv)
{
	_vala_main ();
	return 0;
}
```

可见，Vala语言将这个在C99及以后的C语言标准中都语法正确的for循环转化成了一个较为复杂的while循环，并且使用的是`while (TRUE)`和`if (!condition) {break;}`的结构。Vala编译器还将`i+=1`编译到了一个条件判断内，以便控制在首次循环时不执行`i+=1`。这些都使得Vala生成的C代码较为冗长，可读性较低。

## 目前的发展

虽然Vala语言已经诞生了十多年，但其仍在在积极的发展中，不断有新功能引入。在今年的大版本更新中，Vala也加入了大量的、实用的新功能，以下是部分新功能举例：

自`0.56.0`起，Vala支持了在一个函数内部声明嵌套函数：
```vala
void write_streams (OutputStream stream1, OutputStream stream2, uint8[] data) {
    void nested_function (Object object_source, AsyncResult result) {
        OutputStream stream = object_source as OutputStream;
        try {
            ssize_t size = stream.write_async.finish (result);
            stdout.printf (@"Written $size bytes\n");
        } catch (Error e) {
            stderr.printf ("Error writing to stream: %s", e.message);
        }
    }

    stream1.write_async (data, nested_function);
    stream2.write_async (data, nested_function);
}
```

这样的嵌套函数只在局部有效，与`lambda`函数类似，嵌套函数可以访问上层函数中的变量，除此之外，还可以在其有效范围内多次调用，弥补了`lambda`函数一般只能“一次性使用”的不足。

自`0.56.0`起，Vala还引入了部分类的功能，可以将一个`class`分为多个部分，甚至放到多个文件中进行声明，只需要在声明时加入`partial`关键字：
```vala
public partial class Foo : Object {
    public double bar { get; set; }
}

public partial class Foo : Initable {
    public virtual bool init (Cancellable? cancellable = null) {
        stdout.printf ("hello!\n");
        this.bar = 0.56;
        return true;
    }
}
```

在`0.56.0`中，Vala还为`Sequence`和`Array`数据结构增加了`foreach`的支持，对使用这些数据结构的人带来了更多的便利。

而在下一个版本中，Vala还计划为[ISO 646](http://www.cplusplus.com/reference/ciso646/)提供支持，从语法上带来更好的体验。

笔者也于近期为Vala写了支持用下划线分隔数字以增强可读性的功能，该功能已经[提交PR](https://gitlab.gnome.org/GNOME/vala/-/merge_requests/263)，等待官方的审核与合并。该功能增加了对以下写法的支持：
```vala
int a = 123_456_789;
uint b = 1_000_0000;
double c = 1_500.200_2e-1_000;
```

但由于之前Vala需要支持形如`123_456`的变量名，因此这一功能至少不能很快被合并。笔者为此还提交了[另一个PR](https://gitlab.gnome.org/GNOME/vala/-/merge_requests/270)，通过引入编译时警告来逐步解决这一问题。

笔者为Vala下一个版本提供的[逐字字符串模板功能](https://gitlab.gnome.org/GNOME/vala/-/merge_requests/266)则已经得到合并，该功能支持将Vala的逐字字符串与模板字符串结合使用，其关系类似于Python的`rf""`之于`r""`和`f""`，进一步增强了Vala的字符串处理功能，该功能将两者功能结合，在模板字符串中将`\`视为`\`字符本身，而非转义字符，例如这样的表达：
```vala
void main () {
    double pi = 3.141592653589;
    var s = @"""\pi/ \wow/ \$pi/""";
    print (s);
    // Output:
    // \pi/ \wow/ \3.141592653589/
}
```

由此可见，直到现在，Vala仍然会积极地加入新功能，保持这门现代语言的先进性。

Vala的维护社区虽然参与人数不及众多热门语言，但Vala社区仍较为活跃，探讨氛围良好。从上文[有关编译器bug的部分内容](/2022/10/17/探索Vala语言/#头文件包含问题gtype的使用与glib的包含已修复)可以发现，在笔者撰写这篇博客的短短的时间里，就有bug从反馈到完成了修复的过程，而这也要归功于社区的积极相应。这样良好的社区环境也进一步确保了Vala语言不会落伍。

## 示例

## 捐赠

|  **支付宝**  |  **微信支付**  |
|  :----:  |  :----:  |
|  [![](/img/donate-alipay.webp)](/img/donate-alipay.webp)  |  [![](/img/donate-wechatpay.webp)](/img/donate-wechatpay.webp)  |
